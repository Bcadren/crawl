###############################################################################
# gauntlet.des: Vaults particular to Gauntlets.
#
# <<1>>      Gauntlet arena subvaults.
# <<2>>      Gauntlet exit subvaults.
# <<3>>      Gauntlet main maps.
###############################################################################

: crawl_require("dlua/gauntlet.lua")
{{
function gauntlet_portal(e)
  local timeout_turns = crawl.random_range(400, 600)

  local messager =
    timed_msg {
      initmsg = { "You feel a sense of challenge.",
                  "Hark! There is a gateway to a gauntlet on this level. " ..
                  "Find the entrance quickly before the gate is sealed!" },
      finalmsg = "After a thunderous strike, the drumbeats cease.",
      verb = 'beating',
      noisemaker = 'drum',
      ranges = {
        { 5000, 'faint ' },  { 4000, '' }, { 2500, 'loud ' },
        { 1500, 'thunderous ' }, { 0, 'deafening ' }
      }
    }

    e.lua_marker('O',
      timed_marker {
        turns = timeout_turns,
        msg = messager,
        floor = 'stone_arch',
        feat_tile = 'dngn_portal_gauntlet_gone',
        single_timed = true,
        dstname = 'gauntlet' })
    e.kfeat("O = enter_gauntlet")
    e.tile("O = dngn_portal_gauntlet")
end

function gauntlet_appearance(e)
  e.kfeat("< = exit_gauntlet")
end
}}

#############################################################################
# Gauntlet entry vaults

NAME:   gauntlet_entry_generic
TAGS:   transparent extra uniq_gauntlet
CHANCE: 5%
DEPTH:  D:11-, Orc:$, Lair
: gauntlet_portal(_G)
MAP
O
ENDMAP

default-depth: Gauntlet

#############################################################################
# <<1>> Gauntlet arena subvaults
#
# <<1.1>> 9x9 arenas
# <<1.2>> 7x7 arenas
#############################################################################

#############################################################################
# <<1.1>> 9x9 arenas
#############################################################################
# Each vault should have tags 'gauntlet_arena_first' and
# 'gauntlet_arena_second' assuming its viable for both tiers. Two tags exist in
# case a vault wants to specify custom terrain for specific monsters and loot
# and is only appropriate for one tier.
#
# The vaults must must have the same shape as one of the templates below. These
# all have 'bottom' side that is tapered so that the vaults exit transporter
# landing site in the parent map will be close to the exit transporter. It
# represents the "end" of the vault and is where the exit transporter 'Q'
# glyph, '1' glyph, and 'd' loot glyphs should generally be (see below). As the
# templates below show, the vault can be any rotation of this basic shape.
#
# q.........q  qqqqqq      ..ddQdd..      qqqqqq
# q.........q  .........   ....1....   .........
# q.........q  .........   .........   .........
# q.........q  ........d  q.........q  d........
# q....P....q  ........d  q....P....q  d........
# q.........q  ....P..1Q  q.........q  Q1..P....
#  .........   ........d  q.........q  d........
#  ....1....   ........d  q.........q  d........
#  ..ddQdd..   .........  q.........q  .........
#              .........               .........
#              qqqqqq                     qqqqqq
#
# The 'q' tiles shown # are part of the transparent arena walls and must always
# be present. Ideally nearly every location in the subvault is visible from at
# least one of the three window areas on the walls. The 'P', 'Q', '1', and 'd',
# glyphs don't have to be at the exact spots shown, but the shown spots are
# good guidelines.
#
# All vaults must place the glyphs below in the given quantities and then call
# gauntlet_arena_setup(_G) to set up items, monsters, transporters, and
# terrain.
#
# Transporter Glyphs:
#
# 'P' : Entry transporter landing site. Central locations or locations somewhat
#       far away the top-center (non-tapered side) are best. This is because
#       the player usually enters from a transporter outside the vault at
#       top-center. Monsters inside will gather close to that area, so placing
#       the transporter away from it gives the player turns to move before
#       being attacked. Conversely placing 'P' too close to the exit
#       transporter 'Q' makes it too easy to raid the arena without engaging
#       monsters.
#
# 'Q' : Exit transporter. Needs to be located near the bottom-center (tapered
#       side) of the vault where it will be close to the exit transporter
#       landing site in the parent vault.
#
# Monster and Item Glyphs:
#
# Note that the '1', '2', and '3' glyphs only determine possible locations of
# monsters in the vault, not how many can or will place. See the comments for
# and contents of tier1_gauntlet_arenas in dat/dlua/gauntlet.lua for those
# details.
#
# '1' : At least one glyph for the strongest type of monster. Should be an
#       ideal location if the monster is stationary (i.e. statue or oklob). If
#       multiple '1' exist, one is chosen randomly and the rest become floor.
#
# '2' : At least six of this glyph for the middle tier monsters. Unused
#       instances will become floor.
#
# '3' : At least ten of this glyph for the weakest tier monsters. Unused
#       instances will become floor.
#
# 'd' : At least four of this glyph for loot items. Unused instances will
#       become floor. By default, tier one arenas place two items and tier two
#       place three. See gauntlet_arena_item_setup() for details on default
#       items, and gauntlet_arena_custom_extra_items() to change the default.
#
# 'p' : (Optional) All 'p' glyphs become a plant type appropriate for the
#       chosen monster set.
#
# Terrain Glyphs:
#
# '^' : Vaults should have some '^' glyphs to let the vault have a chance of
#       placing dispersal traps. These should be placed at interesting
#       locations that make the vault more challenging. All instances of '^'
#       become a trap, so any location randomization must be handled in the
#       vault before gauntlet_arena_setup() is called.
#
#       The number of '^' placed shouldn't be excessive for a 9x9 arena. It's
#       helpful to guarantee a trap-free walkable path to 'Q' so that the
#       player isn't forced to repeatedly step on a trap to blink to the exit
#       after the vault is cleared.
#
# 'x' : (Optional) All rock wall glyphs get collectively randomized to
#       appropriate walls for the tier: either rock or stone for first tier and
#       stone, metal, or crystal for the second tier. To prevent this, give a
#       second argument of true to gauntlet_arena_setup(). Otherwise, it's best
#       to use 'x' for all opaque walls in the arena.
#
# 'w' : (Optional) There's a chance for all 'w' glyphs to become 'l' lava
#       glyphs unless the chosen monster set indicates that only water is
#       thematically appropriate.
#
# 'l' : (Optional) All 'l' glyphs get turned to deep water if the chosen
#       monster set indicated that only lava is thematically appropriate.
#############################################################################

NAME:   gammafunk_gauntlet_arena_open
TAGS:   gauntlet_arena_first gauntlet_arena_second
WEIGHT: 5
NSUBST: "' = 10=3, - = 6=2 / ^...
SUBST:  ' = ^.....
: gauntlet_arena_setup(_G)
MAP
q"""""""""q
q"""""""""q
q'''''''''q
q'''...'''q
q'''.P.'''q
q'''...'''q
 ---------
 ----1----
 --ddQdd--
ENDMAP

NAME:   gammafunk_gauntlet_arena_corners
TAGS:   gauntlet_arena_first gauntlet_arena_second
SUBST:  ~ = ^..
NSUBST: ! = ^. / .
: gauntlet_arena_setup(_G)
MAP
q3.3.3.3.3q
q~xx3.3xx~q
q3x3xnx3x3q
q.~xx~xx~.q
q....P....q
q.^xx~xx^.q
 2x2xnx2x2
 !xx.1.xx!
 .2ddQdd2.
ENDMAP

NAME:  gammafunk_gauntlet_arena_hedges
TAGS:  gauntlet_arena_first gauntlet_arena_second
SUBST: r = pp~, ~ = ^..
: gauntlet_arena_setup(_G)
MAP
q3.3.3.3.3q
q~nnn3nnn~q
q3nxrrrxn3q
q~nnn~nnn~q
q3..2P2..3q
q.nnn~nnn~q
 2nxrrrxn2
 ^nnn1nnn^
 .2ddQdd2.
ENDMAP

NAME:  gammafunk_gauntlet_arena_split_x
TAGS:  gauntlet_arena_first gauntlet_arena_second
SUBST: ~ = ^..
: gauntlet_arena_setup(_G)
MAP
q3.3.3.3.3q
q~x3.3.3x~q
q.xx3.3xx.q
q.~xx~xx~.q
q....P....q
q.^xxnxx^.q
 .xx2.2xx.
 !x~.1.~x!
 22ddQdd22
ENDMAP

NAME:   gammafunk_gauntlet_arena_grid
TAGS:   gauntlet_arena_first gauntlet_arena_second
NSUBST: - = 10=3 / ^...., ' = 6=2 / ^..
: gauntlet_arena_setup(_G)
MAP
q---------q
q-x-x-x-x-q
q--x-n-x--q
q-x-x-x-x-q
q--n-P-n--q
q-x-x-x-x-q
 ''x^n^x''
 'x'x1x'x'
 ''ddQdd''
ENDMAP

NAME:   gammafunk_gauntlet_arena_split_diamond
TAGS:   gauntlet_arena_first gauntlet_arena_second
SUBST:  ~ = ^..
NSUBST: ! = ^. / .
: gauntlet_arena_setup(_G)
MAP
q333333333q
q3^xx~xx^3q
q.xx~~~xx.q
q~x.....x~q
q....P....q
q!x.....x!q
 2xx~~~xx2
 2^xx1xx^2
 .2ddQdd2.
ENDMAP

NAME:   nicolae_gauntlet_arena_t_cross
TAGS:   gauntlet_arena_first gauntlet_arena_second
SUBST:  ~ = ^..
NSUBST: P = P / ., ! = ^. / .
: gauntlet_arena_setup(_G)
MAP
q333333333q
q3.~nnn~.3q
q.~..x..~.q
q.n.PxP.n.q
q!nxxxxxn!q
q.n..x..n.q
 .~.2x2.~.
 2^~n1n~^2
 .2ddQdd2.
ENDMAP

NAME:  nicolae_gauntlet_arena_x
TAGS:  gauntlet_arena_first gauntlet_arena_second
SUBST: ~ = ^..
: gauntlet_arena_setup(_G)
MAP
qx3333333xq
q3x.....x3q
q3~x...x~3q
q..~xPx~..q
q...^x^...q
q..~x.x~..q
 .~x2.2x~.
 ~x2.1.2x~
 x2ddQdd2x
ENDMAP

NAME:   nicolae_gauntlet_arena_flowing_pool
TAGS:   gauntlet_arena_first gauntlet_arena_second no_pool_fixup
NSUBST: P = P / -, - = 10=3 / ^..., ' = 6=2 / ^..
: gauntlet_arena_setup(_G)
MAP
q.-ww-..w.q
q-w--w--w-q
qPw-ww-wwPq
qw-w---w--q
qw--wPw-wwq
q-w-www-w-q
 'ww^'^w''
 'w''1w'w'
 d'wdQd'wd
ENDMAP

NAME:   nicolae_gauntlet_arena_quarters
TAGS:   gauntlet_arena_first gauntlet_arena_second
SUBST:  ~ = ^..
NSUBST: P = P / ., ! = ^. / .
: gauntlet_arena_setup(_G)
MAP
q333333333q
q..nx3xn..q
q.n.~x~.n.q
q.x.PxP.x.q
q.^xxxxx^.q
q.x..x..x.q
 !n2~x~2n!
 .2nx1xn2.
 2.ddQdd.2
ENDMAP

NAME:   nicolae_gauntlet_arena_blocks
TAGS:   gauntlet_arena_first gauntlet_arena_second
SUBST:  ~ = ^..
NSUBST: ! = ^. / .
: gauntlet_arena_setup(_G)
MAP
q3.3.3.3.3q
q~x.333.x~q
q3~xxnxx~3q
q..xx~xx..q
q....P....q
q..xx~xx..q
 2^xxnxx^2
 !x.212.x!
 .2ddQdd2.
ENDMAP

NAME:   nicolae_gauntlet_arena_grate_2
TAGS:   gauntlet_arena_first gauntlet_arena_second
KFEAT:  r = iron_grate
NSUBST: "' = 10=3, - = 6=2 / ^.., ! = ^. / .
SUBST:  r = rr^, ' = ^...
: gauntlet_arena_setup(_G)
MAP
q"""""""""q
q'xrx'''x'q
q...xnx'''q
q!x...xrx'q
q.xrxP..x'q
q---xnx...q
 -x---xrx.
 -xrx1--x!
 --ddQdd--
ENDMAP

NAME:   nicolae_gauntlet_swirl
TAGS:   gauntlet_arena_first gauntlet_arena_second
NSUBST: P = P / -, - = 10=3 / ., ' = 6=2 / ., ! = ^.. / .
SUBST:  ~ = ^.
: gauntlet_arena_setup(_G)
MAP
q--x~-----q
q---x--~--q
q-~-x~---xq
q---PnPxx!q
q--^nnn^--q
q!xx'n''''q
 x'''~x'~'
 ''~'1x'''
 d''dQdx'd
ENDMAP

NAME: minmay_gauntlet_arena_make_your_own_arena
TAGS: gauntlet_arena_first gauntlet_arena_second
: gauntlet_arena_setup(_G)
MAP
q..=.P.=..q
q.^=...=^.q
q==n=n=n==q
q..=...=..q
q..=...=..q
q==n...n==q
 33n===n33
 322212223
 33ddQdd33
ENDMAP

NAME:   gammafunk_gauntlet_arena_teeth
TAGS:   gauntlet_arena_first gauntlet_arena_second
SUBST:  ~ = ^...
NSUBST: ! = ^. / .
: gauntlet_arena_setup(_G)
MAP
q..33333..q
q..33333..q
q.nnnnnnn.q
qx~x~x~x~xq
q....P....q
q.x~x~x~x.q
 !nnnnnnn!
 2^2.1.2^2
 .2ddQdd2.
ENDMAP

NAME:    gammafunk_gauntlet_arena_mazey
TAGS:    gauntlet_arena_first gauntlet_arena_second
NSUBST:  P = P / a
# Ensure that at least one of the four paths is always open.
SHUFFLE: rsuv
SUBST:   a = ^pnn, v = ., rsu = ^pnnnnnn
NSUBST:  . = 10=3 / .
: gauntlet_arena_setup(_G)
MAP
q..x.nx..xq
qax..n.nxaq
qx.xa.xn.Pq
qvP..x..a.q
qxvxxrPsxuq
q...rxxs2uq
 x2xnnn22x
 22xa1ndxa
 a^ddQdx^x
ENDMAP

NAME:   gammafunk_gauntlet_arena_chambers
TAGS:   gauntlet_arena_first gauntlet_arena_second
SUBST:  ~ = ^..
NSUBST: ! = ^. / .
: gauntlet_arena_setup(_G)
MAP
q333x.x333q
q33x...x33q
q.xx...xx.q
qx~~x~x~~xq
q....P....q
qx..x^x..xq
 !xxxnxxx!
 22x~1~x22
 .2ddQdd2.
ENDMAP

NAME:    gammafunk_gauntlet_arena_crates
TAGS:    gauntlet_arena_first gauntlet_arena_second
# At least one open path, but usually not the one adjacent to the landing site.
SHUFFLE: Pv / Rr / Ss
SUBST:   a = ...^nn, v = ^nnn, r = .^nn, sRS = ., x = ^ x:150
: gauntlet_arena_setup(_G)
MAP
qxx3xx3xx3q
qxx3xx3xx3q
qa3a33a33aq
qPxxRxxSxxq
q.xx.xx.xxq
q.a.a..a.aq
 x2nxrxn2x
 xvnn1nnsx
 22ddQdd22
ENDMAP

NAME:  gammafunk_gauntlet_arena_arrow_garden
TAGS:  gauntlet_arena_first gauntlet_arena_second
SUBST: ~ = ^..
: gauntlet_arena_setup(_G)
MAP
q.333n333.q
q.33xpx33.q
q.~xpPpx~.q
q~np.n.pn~q
qxp.xpx.pxq
q..xp^px..q
 ^xp2n2px^
 xp2x1x2px
 ddx2Q2xdd
ENDMAP

NAME:  gammafunk_gauntlet_arena_spirals
TAGS:  gauntlet_arena_first gauntlet_arena_second
SUBST: v = ^ n:190
: gauntlet_arena_setup(_G)
MAP
qddxx..33^q
qdvx.vxx^3q
qdx.xv.xx3q
q1x.x.x.x3q
q2v.vPv.v2q
q2x.x.x.x2q
 2xx.vx.x2
 3^xxv.xv1
 ^3333xxQx
ENDMAP

#############################################################################
# <<1.2>> 7x7 arenas
#############################################################################

#############################################################################
# The 7x7 arenas use the same setup function and set of required and optional
# glyphs as the 9x9 vaults described in <<1.1>>. Follow the instructions in
# that section for creating 7x7 arenas, aside from the required tags. For 7x7
# arenas, the the selection tags are gauntlet_arena_77_first and
# gauntlet_arena_77_second. Vaults should have both of these tags unless they
# place specific terrain for a fixed monster set appropriate for only one of
# the tiers.
#
# Since 7x7 arenas are significantly smaller than 9x9 ones, they have some
# design considerations outlined below by minmay:
#
# Arenas have 23 mandatory glyphs, which is almost half of the available area,
# so these are heavily constrained in how many walls/liquids/etc. can be
# placed.
#
# A smaller arena is inherently more dangerous than a larger one, so compensate
# by providing good terrain, to keep the difficulty in line with the larger
# arenas.
#
# Template:
#
# q.......q
# q.......q
# q.......q
# q.......q
# q.......q
#  .......
#  .......
#

NAME:   minmay_gauntlet_arena_77_fan
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q2223xx3q
qx1^nn3.q
qxn3x3^.q
q3nx3xnPq
q2^3x3nxq
 23nn^dx
 2xxQddd
ENDMAP

NAME:   minmay_gauntlet_arena_77_two_lines
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q1.xx333q
q...xx33q
q2x.^xx3q
q2xxP.x3q
q22xx..3q
 2ddxx.3
 2ddQxx3
ENDMAP

NAME:   minmay_gauntlet_arena_77_checkered
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
qx2x3x2xq
q2n2n2n2q
qx3x3x3xq
q3nPn1n3q
qx3x^x3xq
 3ndndn3
 xdxQxdx
ENDMAP

NAME:   minmay_gauntlet_arena_77_box
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q333333^q
q3nxxx33q
q2x^..x2q
q2x.P.x2q
q2x..^x2q
 ..xxxn3
 ^ddQdd1
ENDMAP

NAME:   minmay_gauntlet_arena_77_double_cross
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q2......q
q22x....q
q3xxx...q
q33xPx..q
q333xxx.q
 3333x22
 1ddQdd2
ENDMAP

NAME:   minmay_gauntlet_arena_77_museum
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G, true)
MAP
q2223222q
q3G3I3V3q
q^.p.p.^q
q.x.P.c.q
q^.p.p.^q
 3v3b3n3
 1ddQdd3
ENDMAP

NAME:   minmay_gauntlet_arena_77_chump_sandwich
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q3322233q
q2332332q
q3xxnxx3q
q...P...q
q.xxnxx.q
 ...1...
 .ddQdd.
ENDMAP

NAME:   minmay_gauntlet_arena_77_tiled_triplets
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q1xx2xx2q
qx22n22xq
qxn3nn3xq
q33nP^n3q
qx3nn3nxq
 dn33n3d
 dxxQxxd
ENDMAP

NAME:   minmay_gauntlet_arena_77_hold_the_fort
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q3333222q
q3xxnx22q
q3x..nx2q
q..xP.n3q
q.^nx.x3q
 ..^.xx3
 1ddQdd3
ENDMAP

NAME:   minmay_gauntlet_arena_77_cramped
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
qqq222qqq
qqq333qqq
q23nn=32q
q13nPn33q
q3d=nn32q
 qqdddqq
 qqqQqqq
ENDMAP

NAME:   minmay_gauntlet_arena_77_p_blocks
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q33332xxq
q3xxx2xxq
q3xx.^x3q
q22.P.22q
q3x^.xx3q
 xxdxxx3
 xxdQdd1
ENDMAP

# Without the runed door, players could still keep all the monsters away from
# the entry point by running clockwise around the vault. That is tedious, so the
# runed door gets rid of the need for it.
NAME:   minmay_gauntlet_arena_77_path
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q222n^.Pq
q2x2x.xxq
q2n3n^.^q
q1x3xxx.q
qdn333n3q
 dxxx3x=
 ddQn333
ENDMAP

NAME:   minmay_gauntlet_arena_77_mirror
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q2221222q
qxxx.xxxq
q3.n.n.3q
q3xxPxx3q
q3n...n3q
 3x^.^x3
 3ddQdd3
ENDMAP

NAME:   minmay_gauntlet_arena_77_terracotta_army
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q22GxG22q
q233G332q
qG3G^G3Gq
qxG3P3Gxq
qG3G^G3Gq
 ...G...
 1ddQdd.
ENDMAP

NAME:   minmay_gauntlet_arena_77_run_for_it
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q...Pn33q
q.xx.n33q
q3xx.n33q
q33^.n23q
qnn===21q
 ddn.n22
 ddnQn22
ENDMAP

NAME:   minmay_gauntlet_arena_77_squiggly
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q22222x3q
q.xx2.x3q
qx..nn33q
qx3^P^3xq
q33nn..xq
 3xddxx.
 3xdQd.1
ENDMAP

NAME:   minmay_gauntlet_arena_77_radiant_squares
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q3332221q
q.nxnx32q
q^.3^3x2q
qxnxP^n2q
q.ddx3x3q
 xddn.n3
 Qx.x^.3
ENDMAP

NAME:   minmay_gauntlet_arena_77_broken_ladder
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q3nnx333q
q3x333x2q
q33^xnn2q
q2x.P.x2q
q2nnx^.1q
 2x...x.
 .ddQdd.
ENDMAP

NAME:   minmay_gauntlet_arena_77_shame_cube
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q^333333q
q3xnxnx3q
q3=...=3q
q2x.P.x2q
q2nwwwn2q
 2xnxnx2
 1ddQdd^
ENDMAP

NAME:   minmay_gauntlet_arena_77_mess
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
# there is one 3 on the map because a dispersal trap should not be on that
# square
NSUBST: ' = 9=3 / 6=2 / 1 / ^ / .
: gauntlet_arena_setup(_G)
MAP
qn''x'n'q
q'xn'x3nq
qn''xn'xq
qx'xP'x'q
q'n''xn'q
 'xnx''x
 nddQddn
ENDMAP

NAME:   minmay_gauntlet_arena_77_block_sizes
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q222.222q
q3x..xx3q
q3..Pxx3q
q3xnx.^3q
q3xnx^13q
 3xnx..3
 .ddQdd.
ENDMAP

NAME:   minmay_gauntlet_arena_77_braids
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q22n.n22q
q3x2x2x3q
q3x.x.x3q
q^3nPn3^q
q3x.x.x3q
 3x1x.x3
 ddnQndd
ENDMAP

# to escape, face either the 1 or the plant
NAME:   minmay_gauntlet_arena_77_two_paths
TAGS:   gauntlet_arena_77_first gauntlet_arena_77_second
: gauntlet_arena_setup(_G)
MAP
q222.222q
q3xnxnx3q
q^33x33^q
qxnxPxnxq
q^33x33^q
 .xnxnxp
 1ddQdd.
ENDMAP

#############################################################################
# <<2>> Gauntlet exit subvaults
#############################################################################
# The vaults exit subvaults that place an exit, a boss, and its loot pile.
# These vaults must have tag 'gauntlet_exit'. Vaults not defining their own custom
# loot set can place exactly one '<' and call gauntlet_exit_loot(G_) to place
# loot there. Otherwise the map must place its own loot and exit_gauntlet
# feature. The vault must also place exactly one patrolling awake minotaur.
#
# These vaults must have the same shape as the template below. This is so that
# they place in the parent vault with the subvault entrance in the right
# relative location. The entrance must place in the first row at the wider top
# section, relative to the template below.
#
# ...............
# ...............
# ...............
# ...............
# ...............
# ...............
# ...............
# ...............
# ...............
# ...............
#  .............
#  .............
#  .............
#
# The vault will be surrounded by metal walls in the parent map. Hence the
# subvault should fill the shape above with 'v' metal wall glyphs for areas
# that are meant to be part of that surrounding wall.
#############################################################################

# Adapted from the labyrinth_watery minotaur exit vault.
NAME:    gauntlet_exit_watery
TAGS:    gauntlet_exit no_pool_fixup
MONS:    patrolling generate_awake minotaur
KFEAT:   < = exit_gauntlet
SHUFFLE: def, ghi
SUBST:   dg = ., efhi = v
: gauntlet_exit_loot(_G)
MAP
vvv.vvv.vvv.vvv
vvv.vvv.vvv.vvv
v.............v
v.vvv.vvv.vvv.v
v.vvv.vvv.vvv.v
v.............v
vvvvbbb+bbbvvvv
vvvvb.....bvvvv
vvvvbbb1bbbvvvv
vvvvbw...wbvvvv
 vvvbww<wwbvvv
 vvvbwwwwwbvvv
 vvvbbbbbbbvvv
ENDMAP

# Adapted from labyrinth_green.
NAME:   gauntlet_exit_green
TAGS:   gauntlet_exit
MONS:   patrolling generate_awake minotaur
KFEAT:  < = exit_gauntlet
: gauntlet_exit_loot(_G)
MAP
v.vv.vv.vv.vv.v
v.vv.vv.vv.vv.v
...............
.vv.vv.vv.vv.vv
.vv.vv.vv.vv.vv
...............
vv.vv.b+bvv.vv.
vv.vv.b.bvv.vv.
......b.b......
v.vv.vb1bv.vv.v
 .vv.vb<bv.vv.
 .....bbb.....
 vv.vv.vv.vv.v
ENDMAP

# Adapted from labyrinth_spiral.
NAME:   gauntlet_exit_spiral
TAGS:   gauntlet_exit
MONS:   patrolling generate_awake minotaur
SUBST:  b : bvz, z = vb
KFEAT:  < = exit_gauntlet
: gauntlet_exit_loot(_G)
MAP
vvvvvvv.vvvvvvv
vvvvvvv.vvvvvvv
vvv.....vvvvvvv
vvb.bbbbbbbvvvv
vvb.b.....bvvvv
vvb.b.bbb.bvvvv
vvb.b.b<b.bvvvv
vvb.b.b1b.bvvvv
vvb.b.b.b.bvvvv
vvb.b...b.bvvvv
 vb.bbbbb.bvvv
 vb.......bvvv
 vbbbbbbbbbvvv
ENDMAP

# Adapted from labyrinth_hidden_loot.
NAME:   gauntlet_exit_hidden_loot
TAGS:   gauntlet_exit
MONS:   patrolling generate_awake minotaur, minotaur zombie
SUBST:  d = 2%*
SUBST:  b : bvv
KFEAT:  < = exit_gauntlet
: gauntlet_exit_loot(_G)
MAP
vvv.vvv.vvv.vvv
vvv.vvv.vvv.vvv
v.............v
v.vvv.vvv.vvv.v
v.............v
vvvvxxx+xxxxvvv
vvvvxxx.xxxxvvv
vvvvxx..<.xxvvv
vvvvd+U...xxvvv
vvvvxx...U+dvvv
 vvvxx.U.1xxvv
 vvvxxx+xxxxvv
 vvvxxxdxxxxvv
ENDMAP

# Adapted from labyrinth_mini_lab.
NAME:    gauntlet_exit_mini_maze
TAGS:    gauntlet_exit
SHUFFLE: def, ghi, klm
SUBST:   d : b, e : ., f : b
SUBST:   g : b, h : ., i : b
SUBST:   k = <, l = ., m = .
KMONS:   < = patrolling generate_awake minotaur
KFEAT:   < = exit_gauntlet
SUBST:   b : vvb
: gauntlet_exit_loot(_G)
# should not be necessary
validate {{ return has_exit_from_glyph('<') }}
MAP
vbfbbbbbebbbdbv
vbfg.......b.bv
vbfb.bhbdb.b.bv
vbfb.b.b.g.b.bv
vbfb.f.b.b.b.bv
vbfbii.b.b...bv
vbfb.h.b.bbebbv
vb...b.b.....bv
vbbbbb.bfbbb.bv
vb.........b.bv
 bfbbdbbbbebgb
 bl....k....mb
 bbbbbbbbbbbbb
ENDMAP

#############################################################################
# Chooses four corpses, each with their own set of loot, themed around some
# particular archetypical species/background. Each set of loot has 2-4 items.
NAME:    nicolae_gauntlet_exit_dead_adventurers
TAGS:    gauntlet_exit
KMONS:   1 = patrolling generate_awake minotaur
KPROP:   DELM'1 = bloody
KFEAT:   ' = floor
SHUFFLE: bvc, DELM, DEFHJK, LMNOPQ
{{
-- These adventurers always place a weapon.

local light_scales = "troll leather good_item" ..
    " / stream dragon scales good_item / acid dragon scales good_item"
local medium_scales = "swamp dragon scales good_item" ..
    " / quicksilver dragon scales / fire dragon scales good_item" ..
    " / ice dragon scales good_item / quicksilver dragon scales" ..
    " / pearl dragon scales good_item"
local heavy_scales = "storm dragon scales / shadow dragon scales" ..
    " / gold dragon scales"
local heavy_armour = "scale mail good_item w:50 / chain mail good_item w:50" ..
    " / plate armour good_item w:50 / crystal plate good_item"

 -- Dwarven fighter. Maybe the minotaur got the last MD.
dgn.delayed_decay_extra(_G, "D", "dwarf skeleton",
    "flail randart / morningstar good_item / eveningstar w:2" ..
    "    / demon whip w:2 / dire flail good_item / great mace good_item" ..
    "    / war axe randart / broad axe good_item / battle axe good_item" ..
    "    / executioner's axe w:2 " ..
    ", ring mail randart w:50 / " .. heavy_armour .. " / " .. medium_scales ..
    "    / " .. heavy_scales ..
    ", heater shield good_item / scutum good_item / " .. dgn.good_aux_armour ..
    "    / nothing w:80" ..
    ", any wand w:20 / any misc / gold / any scroll / any potion" ..
    "    / nothing w:60")

 -- Centaur hunter.
dgn.delayed_decay_extra(_G, "E", "centaur skeleton",
    "shortbow good_item / longbow w:5 / arbalest good_item" ..
    "    / triple crossbow w:5" ..
    ", leather armour good_item w:50 / ring mail good_item w:50" ..
    "    / " .. heavy_armour .. " / " .. medium_scales ..
    "    / " .. heavy_scales ..
    ", centaur barding good_item / " .. dgn.good_aux_armour ..
    "    / nothing w:70" ..
    ", any wand w:20 / any misc / gold / any scroll / any potion" ..
    "    / nothing w:60")

 -- Spriggan stabber.
dgn.delayed_decay_extra(_G, "F", "spriggan skeleton",
    "dagger good_item / short sword good_item / rapier good_item" ..
    "    / quick blade w:5" ..
    ", robe good_item w:120 / " .. light_scales .. " / shadow dragon scales" ..
    ", randbook disc:hexes / book of maledictions / nothing w:20" ..
    ", buckler good_item / hat good_item / any wand w:20 / any misc / gold" ..
    "    / any scroll / any potion / nothing w:80")

 -- Merfolk skald. 3 guaranteed items, 3.25 on-average.
dgn.delayed_decay_extra(_G, "H", "merfolk skeleton",
    "trident good_item / demon trident / glaive good_item / bardiche" ..
    ", leather armour good_item w:100 / ring mail good_item w:100" ..
    "    / " .. light_scales .. " / " .. medium_scales ..
    ", randbook disc:charms / book of battle" ..
    -- Give this a 1/4 in chance of placing since we guarantee 3 items above.
    ", buckler good_item / heater shield good_item / scutum good_item" ..
    "    / " .. dgn.good_aux_armour .. " / any wand w:20 / any misc / gold" ..
    "    / any scroll / any potion / nothing w:480")

 -- Hill orc fighter. 3 guaranteed items, 3.25 on-average.
dgn.delayed_decay_extra(_G, "J", "orc skeleton",
    "long sword good_item / double sword w:5 / demon blade w:5" ..
    "    / flail good_item / morningstar good_item / eveningstar" ..
    "    / war axe good_item / broad axe good_item / trident good_item" ..
    "    / demon trident w:5" ..
    ", heater shield good_item / scutum good_item w:5" ..
    ", ring mail randart w:50 / " .. heavy_armour .. " / " .. medium_scales ..
    "    / " .. heavy_scales ..
    -- Give this a 1/4 in chance of placing since we guarantee 3 items above.
    ", any wand w:20 / any misc / gold / any scroll / any potion" ..
    "    / " .. dgn.good_aux_armour ..  " / nothing w:390")

 -- Ogre, maybe an ogre mage of some kind.
dgn.delayed_decay_extra(_G, "K", "ogre skeleton",
    "trident good_item / demon trident / glaive good_item / bardiche" ..
    "    / war axe randart / broad axe good_item / battle axe good_item" ..
    "    / executioner's axe w:2 / giant club good_item" ..
    "    / giant spiked club good_item / quarterstaff good_item / lajatang" ..
    "    / any magical staff w:20" ..
    ", robe good_item w:200 / " .. light_scales .. " / " .. medium_scales ..
    "    / " .. heavy_scales ..
    ", hat good_item w:20 / cloak good_item w:20 / heater shield good_item" ..
    "    / scutum good_item / nothing w:60" ..
    ", randbook w:20 / any wand w:20 / any misc / gold / any scroll" ..
    "    / any potion / nothing w:80")

-- These adventurers don't always place weapons, with the exception of
-- magical staves.

-- Human wanderer.
dgn.delayed_decay_extra(_G, "L", "human skeleton",
    "any armour good_item, any weapon good_item / randbook / any misc" ..
    ", any book / any jewellery / any wand w:20 / any misc / gold" ..
    "    / any scroll / any potion / nothing w:70" ..
    ", any book / any jewellery / any wand w:20 / any misc / gold" ..
    "    / any scroll / any potion / nothing w:70")

-- Deep Elf caster.
dgn.delayed_decay_extra(_G, "M", "elf skeleton",
    "randbook, any magical staff" ..
    ", robe good_item w:50 / leather armour good_item w:50 " .. light_scales ..
    "    / buckler good_item / " .. dgn.good_aux_armour ..
    "    / nothing" ..
    ", any jewellery w:20 / any wand w:20 / any misc / gold" ..
    "    / any scroll / any potion / nothing w:80")

-- Deep Dwarf artificer.
dgn.delayed_decay_extra(_G, "N", "dwarf skeleton",
    "any wand, any misc, any wand / any misc / nothing w:20" ..
    ", any weapon good_item / any amour good_item / gold / any scroll" ..
    "    / any potion / nothing w:50")

-- Octopode. Two guaranteed rings and up to two more.
dgn.delayed_decay_extra(_G, "O", "octopode corpse",
    "any ring good_item, any ring good_item" ..
    ", any ring w:150 / any magical staff / trident good_item" ..
    "    / demon trident w:5 / glaive good_item w:5 / nothing w:180" ..
    ", any ring w:60 / hat good_item w:20 / buckler good_item w:20" ..
    "    / heater shield good_item / scutum good_item / any wand w:20" ..
    "    / any misc / gold / any scroll / any potion / nothing w:180")

-- Felid Transmuter.
dgn.delayed_decay_extra(_G, "P", "felid skeleton",
    "randbook disc:transmutation / book of changes" ..
    "    / book of transfigurations" ..
    ", any jewellery good_item w:60 / manual of transmutations" ..
    "    / manual of unarmed combat / manual of fighting" ..
    ", any wand w:20 / any misc / gold / any scroll / any potion" ..
    "    / nothing w:60" ..
    ", any wand w:20 / any misc / gold / any scroll / any potion" ..
    "    / nothing w:60")

-- Halfling warper.
dgn.delayed_decay_extra(_G, "Q", "halfling skeleton",
    "randbook disc:translocation / book of spatial translocations" ..
    "    / book of the warp" ..
    ", scroll of blinking / scroll of teleportation q:2" ..
    "    / scroll of random uselessness" ..
    ", dagger good_item / short sword good_item / rapier good_item" ..
    "    / long sword good_item / double sword / demon blade" ..
    "    / hunting sling randart / fustibalus good_item" ..
    "    / hand crossbow randart / arbalest good_item / triple crossbow" ..
    "    / shortbow good_item / any armour good_item / nothing w:130" ..
    ", buckler good_item / heater shield good_item / " .. dgn.good_aux_armour ..
    "    / any wand w:20 / any misc / gold / any scroll / any potion" ..
    "    / nothing w:140")

}}
MAP
v.v.vvvvvvv.v.v
v.v....v....v.v
v.v.vv.v.vv.v.v
v.............v
v.v..bb+bb..v.v
v.v..bG.Gb..v.v
v....b...b....v
vvvG.bG1Gb.Gvvv
vvvvvbE'Mbvvvvv
vvvvvbG'Gbvvvvv
 vvvvbD'Lbvvvv
 vvvvbG<Gbvvvv
 vvvvbbbbbvvvv
ENDMAP

#############################################################################
# <<3>> Gauntlet main maps.
#############################################################################
# These are encompass maps that place the arena and exit subvaults. These maps
# should show the player all arenas and give the player some kind of path
# through those arenas based on choice. The number of arenas taken should be
# anywhere from 2-4 to avoid these from completely overshadowing the reward
# from the boss fight.
#
# Before any arena subvault statements, call the setup function:
#
#    gauntlet_setup(_G, entry_glyphs, exit_glyphs)
#
# The map should place the arena entry transporters on the glyphs in the string
# 'entry_glyphs' and the arena exit transporter landing sites on the glyphs in
# the string 'exit_glyphs'.

# The glyphs in these strings must be in the same order of the subvault
# transporter satements so that the transporters go to their intended
# destinations.
#
# Gauntlet arenas have two tiers for easier and harder difficulties. To set a
# subvault's difficulty, call gauntlet_arena_set_tier(n) before the subvault
# statement and use tags gauntlet_arena_first and gauntlet_arena_second. The
# gauntlet_setup() function will initialize difficulty to the first tier.
#
# The map should place exactly one subvault with tag 'gauntlet_exit', which
# will place and exit features. Call gauntlet_appearance(_G) to set the exit
# feature on the '<' and for any other common visual changes.

NAME:   gammafunk_gauntlet_branching
ORIENT: encompass
TAGS:   no_item_gen no_monster_gen no_trap_gen
: gauntlet_setup(_G, "DFJLNPRp", "EHKMOQSt")
SUBVAULT: a : gauntlet_arena_first
SUBVAULT: d : gauntlet_arena_first
: if crawl.coinflip() then
SUBVAULT: e : gauntlet_arena_first
SUBVAULT: f : gauntlet_arena_first
SUBVAULT: g : gauntlet_arena_first
SUBVAULT: h : gauntlet_arena_first
: else
:     gauntlet_arena_set_tier(2)
SUBVAULT: e : gauntlet_arena_second
SUBVAULT: f : gauntlet_arena_second
SUBVAULT: g : gauntlet_arena_second
SUBVAULT: h : gauntlet_arena_second
: end
: gauntlet_arena_set_tier(2)
SUBVAULT: i : gauntlet_arena_second
SUBVAULT: j : gauntlet_arena_second
SUBVAULT: y : gauntlet_exit
: gauntlet_appearance(_G)
MAP
               ccc ccc ccccccccccccc ccc ccc
               cGc cGc cGccccGccccGc cGc cGc
              ccqcccqcccqccccqccccqcccqcccqcc
              cl............l<l............lc
            ccc.............................ccc
            cGq..qqqqaaaaaaq...qddddddqqqq..qGc
            cqq..qaaaaaaaaaq...qdddddddddq..qqc
            c....qaaaaaaaaaq.A.qdddddddddq....c
          ccc....qaaaaaaaaaq...qdddddddddq....ccc
          cGq..qqqaaaaaaaaaq...qdddddddddqqq..qGc
          ccc..qEqaaaaaaaaaqD.FqdddddddddqHq..ccc
            c..q.qaaaaaaaaaq...qdddddddddq.q..c
          ccc..q.qaaaaaaaaaq...qdddddddddq.q..ccc
          cGq..q.qaaaaaaaaaq...qdddddddddq.q..qGc
    cccccccqq..q.qaaaaaaaaaq...qdddddddddq.q..qqccccccc
    cGq........q.qqqqaaaaaaq...qddddddqqqq.q........qGc
  cccqq........q.q...........l...........q.q........qqccc
cccl.....qqqqqqq.qqqqqqq....lGl....qqqqqqq.qqqqqqq.....lccc
cGq......qJ...........Lq.....l.....qN...........Pq......qGc
ccc..qqqqqqqqqqqqqqqqqqqqqqq...qqqqqqqqqqqqqqqqqqqqqqq..ccc
  c..eeeeeeeeeee.fffffffffff...ggggggggggg.hhhhhhhhhhh..c
ccc..eeeeeeeeeee.fffffffffff...ggggggggggg.hhhhhhhhhhh..ccc
cGq..eeeeeeeeeee.fffffffffff...ggggggggggg.hhhhhhhhhhh..qGc
ccc..eeeeeeeeeee.fffffffffff...ggggggggggg.hhhhhhhhhhh..ccc
  c..eeeeeeeeeee.fffffffffff...ggggggggggg.hhhhhhhhhhh..c
ccc..eeeeeeeeeee.fffffffffff...ggggggggggg.hhhhhhhhhhh..ccc
cGq..qeeeeeeeeeq.qfffffffffq...qgggggggggq.qhhhhhhhhhq..qGc
ccc..qeeeeeeeeeq.qfffffffffq...qgggggggggq.qhhhhhhhhhq..ccc
  c..qeeeeeeeeeq.qfffffffffq...qgggggggggq.qhhhhhhhhhq..c
ccc..qqqqqqqqqqqqqqqqqqqqqqq...qqqqqqqqqqqqqqqqqqqqqqq..ccc
cGq......qK...........Mq.....l.....qO...........Qq......qGc
cccl.....qqqqqqq.qqqqqqq....lGl....qqqqqqq.qqqqqqq.....lccc
  cccqq........q.q...........l...........q.q........qqccc
    cGq........q.qiiiiiiqqqq...qqqqjjjjjjq.q........qGc
    cccccccqq..q.qiiiiiiiiiq...qjjjjjjjjjq.q..qqccccccc
          cGq..q.qiiiiiiiiiq...qjjjjjjjjjq.q..qGc
          ccc..q.qiiiiiiiiiq...qjjjjjjjjjq.q..ccc
            c..q.qiiiiiiiiiq...qjjjjjjjjjq.q..c
          ccc..qRqiiiiiiiiiq...qjjjjjjjjjqpq..ccc
          cGq..qqqiiiiiiiiiq...qjjjjjjjjjqqq..qGc
          ccc....qiiiiiiiiiq...qjjjjjjjjjq....ccc
            c....qiiiiiiiiiq...qjjjjjjjjjq....c
            cqq..qiiiiiiiiiq...qjjjjjjjjjq..qqc
            cGq..qiiiiiiqqqq...qqqqjjjjjjq..qGc
            ccc........qSq.......qtq........ccc
              cl.......q.q..l<l..q.q.......lc
              ccqccccqqq.qqqqqqqqq.qqqccccncc
               cGc  vl...............lv  cGc
               ccc  vG.G.G.G.G.G.G.G.Gv  ccc
                    vl...............lv
                    vvyyyyyyyyyyyyyyyvv
                     vyyyyyyyyyyyyyyyv
                     vyyyyyyyyyyyyyyyv
                     vyyyyyyyyyyyyyyyv
                     vyyyyyyyyyyyyyyyv
                     vyyyyyyyyyyyyyyyv
                     vyyyyyyyyyyyyyyyv
                     vyyyyyyyyyyyyyyyv
                     vyyyyyyyyyyyyyyyv
                     vyyyyyyyyyyyyyyyv
                     vvyyyyyyyyyyyyyvv
                     vvyyyyyyyyyyyyyvv
                     vvyyyyyyyyyyyyyvv
                     vvvvvvvvvvvvvvvvv
ENDMAP

NAME:   gammafunk_gauntlet_hard_choice
ORIENT: encompass
TAGS:   no_item_gen no_monster_gen no_trap_gen
: gauntlet_setup(_G, "DFJLNP", "EHKMOQ")
SUBVAULT: a : gauntlet_arena_first
SUBVAULT: p : gauntlet_arena_first
# 50% chance to make the second fight harder.
: if crawl.coinflip() then
SUBVAULT: z : gauntlet_arena_first
SUBVAULT: r : gauntlet_arena_first
: else
:   gauntlet_arena_set_tier(2)
SUBVAULT: z : gauntlet_arena_second
SUBVAULT: r : gauntlet_arena_second
: end
: gauntlet_arena_set_tier(2)
SUBVAULT: s : gauntlet_arena_second
SUBVAULT: u : gauntlet_arena_second
SUBVAULT: y : gauntlet_exit
: gauntlet_appearance(_G)
MAP
ccccccccccccccccccccccccccccccccccc
cccccGcccGcccGcccGcccGcccGcccGccccc
cccccqcccqcccqcccqcccqcccqcccqccccc
cccl............l<l............lccc
cGq.......D.............F.......qGc
ccc..qqqqqqqqqqq...qqqqqqqqqqq..ccc
ccc..aaaaaaaaaaa...ppppppppppp..ccc
ccc..aaaaaaaaaaa.A.ppppppppppp..ccc
cGq..aaaaaaaaaaa...ppppppppppp..qGc
ccc..aaaaaaaaaaa...ppppppppppp..ccc
ccc..aaaaaaaaaaa...ppppppppppp..ccc
ccc..aaaaaaaaaaa...ppppppppppp..ccc
cGq..qaaaaaaaaaq...qpppppppppq..qGc
ccc..qaaaaaaaaaq...qpppppppppq..ccc
ccc..qaaaaaaaaaq...qpppppppppq..ccc
ccc..qqqqqqqqqqq...qqqqqqqqqqq..ccc
cGq.....q.E.q....l....q.H.q.....qGc
ccc...G.q...q...lGl...q...q.G...ccc
ccc.....q.J.q....l....q.L.q.....ccc
ccc..qqqqqqqqqqq...qqqqqqqqqqq..ccc
cGq..zzzzzzzzzzz...rrrrrrrrrrr..qGc
ccc..zzzzzzzzzzz...rrrrrrrrrrr..ccc
ccc..zzzzzzzzzzz...rrrrrrrrrrr..ccc
ccc..zzzzzzzzzzz...rrrrrrrrrrr..ccc
cGq..zzzzzzzzzzz...rrrrrrrrrrr..qGc
ccc..zzzzzzzzzzz...rrrrrrrrrrr..ccc
ccc..qzzzzzzzzzq...qrrrrrrrrrq..ccc
ccc..qzzzzzzzzzq...qrrrrrrrrrq..ccc
cGq..qzzzzzzzzzq...qrrrrrrrrrq..qGc
ccc..qqqqqqqqqqq...qqqqqqqqqqq..ccc
ccc.....q.K.q....l....q.M.q.....ccc
ccc...G.q...q...lGl...q...q.G...ccc
cGq.....q.N.q....l....q.P.q.....qGc
ccc..qqqqqqqqqqq...qqqqqqqqqqq..ccc
ccc..sssssssssss...uuuuuuuuuuu..ccc
ccc..sssssssssss...uuuuuuuuuuu..ccc
cGq..sssssssssss...uuuuuuuuuuu..qGc
ccc..sssssssssss...uuuuuuuuuuu..ccc
ccc..sssssssssss...uuuuuuuuuuu..ccc
ccc..sssssssssss...uuuuuuuuuuu..ccc
cGq..qsssssssssq...quuuuuuuuuq..qGc
ccc..qsssssssssq...quuuuuuuuuq..ccc
ccc..qsssssssssq...quuuuuuuuuq..ccc
ccc..qqqqqqqqqqq...qqqqqqqqqqq..ccc
cGq.............................qGc
cccl............l<l............lccc
cccccqccccqqqqqqqqqqqqqqqccccqccccc
    cGc vl...............lv cGc
    ccc vG.G.G..O.Q..G.G.Gv ccc
        vl...............lv
        vvyyyyyyyyyyyyyyyvv
         vyyyyyyyyyyyyyyyv
         vyyyyyyyyyyyyyyyv
         vyyyyyyyyyyyyyyyv
         vyyyyyyyyyyyyyyyv
         vyyyyyyyyyyyyyyyv
         vyyyyyyyyyyyyyyyv
         vyyyyyyyyyyyyyyyv
         vyyyyyyyyyyyyyyyv
         vyyyyyyyyyyyyyyyv
         vvyyyyyyyyyyyyyvv
         vvyyyyyyyyyyyyyvv
         vvyyyyyyyyyyyyyvv
         vvvvvvvvvvvvvvvvv
ENDMAP

NAME:   gammafunk_gauntlet_shortcut
ORIENT: encompass
TAGS:   no_item_gen no_monster_gen no_trap_gen
: gauntlet_setup(_G, "DFJLNP", "EHKMOQ")
: gauntlet_arena_set_tier(2)
# 3.5 on average per arena (including the single guaranteed item), for 7 items
# total on average.
: gauntlet_arena_custom_extra_items("fg / |* / e.")
SUBVAULT: a : gauntlet_arena_second
SUBVAULT: d : gauntlet_arena_second
: gauntlet_arena_set_tier(1)
# 1.75 on average per arena for 7 items total on average.
: gauntlet_arena_custom_extra_items("fffggg|||***.... / .")
SUBVAULT: e : gauntlet_arena_first
SUBVAULT: f : gauntlet_arena_first
SUBVAULT: g : gauntlet_arena_first
SUBVAULT: h : gauntlet_arena_first
SUBVAULT: y : gauntlet_exit
: gauntlet_appearance(_G)
MAP
ccccccccccccccccccccccccccccccccccccccccccccccccc
ccccGcccGcccGcccGcccGcccGcccGcccGcccGcccGcccGcccc
ccccqcccqcccqcccqcccqcccqcccqcccqcccqcccqcccqcccc
cccl............l<l..........................lccc
cGq.......D.............J.....................qGc
ccc..qqqqqqqqqqq...qqqqqqqqqqq...qffffffqqqq..ccc
ccc..aaaaaaaaaaa...eeeeeeeeeee.G.qfffffffffq..ccc
ccc..aaaaaaaaaaa.A.eeeeeeeeeee...qfffffffffq..ccc
cGq..aaaaaaaaaaa...eeeeeeeeeeeqqqqfffffffffq..qGc
ccc..aaaaaaaaaaa...eeeeeeeeeee...qfffffffffq..ccc
ccc..aaaaaaaaaaa...eeeeeeeeeeeK.Lqfffffffffq..ccc
ccc..aaaaaaaaaaa...eeeeeeeeeee...qfffffffffq..ccc
cGq..qaaaaaaaaaq...qeeeeeeeeeqqqqqfffffffffq..qGc
ccc..qaaaaaaaaaq...qeeeeeeeeeq...qfffffffffq..ccc
ccc..qaaaaaaaaaq...qeeeeeeeeeq.G.qfffffffffq..ccc
ccc..qqqqqqqqqqq...qqqqqqqqqqq...qffffffqqqq..ccc
cGq.....q.E.q....l.............l....q.M.q.....qGc
ccc...G.q...q...lGl...........lGl...q...q.G...ccc
ccc.....q.F.q....l.............l....q.N.q.....ccc
ccc..qqqqqqqqqqq...qqqqqqqqqqq...qqqqqqqqqqq..ccc
cGq..ddddddddddd...hhhhhhhhhhh.G.ggggggggggg..qGc
ccc..ddddddddddd...hhhhhhhhhhh...ggggggggggg..ccc
ccc..ddddddddddd...hhhhhhhhhhhqqqggggggggggg..ccc
ccc..ddddddddddd...hhhhhhhhhhh...ggggggggggg..ccc
cGq..ddddddddddd...hhhhhhhhhhhP.Oggggggggggg..qGc
ccc..ddddddddddd...hhhhhhhhhhh...ggggggggggg..ccc
ccc..qdddddddddq...qhhhhhhhhhqqqqqgggggggggq..ccc
ccc..qdddddddddq...qhhhhhhhhhq...qgggggggggq..ccc
cGq..qdddddddddq...qhhhhhhhhhq.G.qgggggggggq..qGc
ccc..qqqqqqqqqqq...qqqqqqqqqqq...qqqqqqqqqqq..ccc
  c...........................................c
  cl............l<l..........................lc
  ccccqcccqqqqqqqqqqqqqqqcccqcccqcccqcccqcccqcc
     cGcvl...............lvcGcccGcccGcccGcccGc
     cccvG.G.G..H.Q..G.G.Gvccccccccccccccccccc
        vl...............lv
        vvyyyyyyyyyyyyyyyvv
         vyyyyyyyyyyyyyyyv
         vyyyyyyyyyyyyyyyv
         vyyyyyyyyyyyyyyyv
         vyyyyyyyyyyyyyyyv
         vyyyyyyyyyyyyyyyv
         vyyyyyyyyyyyyyyyv
         vyyyyyyyyyyyyyyyv
         vyyyyyyyyyyyyyyyv
         vyyyyyyyyyyyyyyyv
         vvyyyyyyyyyyyyyvv
         vvyyyyyyyyyyyyyvv
         vvyyyyyyyyyyyyyvv
         vvvvvvvvvvvvvvvvv
ENDMAP

NAME:   gammafunk_gauntlet_one_two_three
ORIENT: encompass
TAGS:   no_item_gen no_monster_gen no_trap_gen
: gauntlet_setup(_G, "DFJLNPRpr", "EHKMOQSst")
SUBVAULT: a : gauntlet_arena_first
SUBVAULT: d : gauntlet_arena_first
SUBVAULT: e : gauntlet_arena_first
: gauntlet_arena_set_tier(2)
SUBVAULT: f : gauntlet_arena_second
SUBVAULT: g : gauntlet_arena_second
SUBVAULT: h : gauntlet_arena_second
SUBVAULT: i : gauntlet_arena_second
SUBVAULT: j : gauntlet_arena_second
SUBVAULT: k : gauntlet_arena_second
SUBVAULT: y : gauntlet_exit
: gauntlet_appearance(_G)
MAP
                                     ccccc
                    ccc ccc ccc ccc  cl<lc  ccc ccc ccc ccc
                    cGc cGc cGc cGc  c...c  cGc cGc cGc cGc
                   ccqcccqcccqcccqcccc.A.ccccqcccqcccqcccqcc
                   cl.....................................lc
                 ccc...................D...................ccc
                 cGq..qqqqddddddq.qqqqqqqqqqq.qeeeeeeqqqq..qGc
                 cqq..qdddddddddq.aaaaaaaaaaa.qeeeeeeeeeq..qqc
                 c....qdddddddddq.aaaaaaaaaaa.qeeeeeeeeeq....c
               ccc....qdddddddddq.aaaaaaaaaaa.qeeeeeeeeeq....ccc
               cGq..qqqdddddddddq.aaaaaaaaaaa.qeeeeeeeeeqqq..qGc
               ccc..qHqdddddddddq.aaaaaaaaaaa.qeeeeeeeeeqKq..ccc
                 c..q.qdddddddddq.aaaaaaaaaaa.qeeeeeeeeeq.q..c
       ccc ccc ccc..q.qdddddddddq.qaaaaaaaaaq.qeeeeeeeeeq.q..ccc ccc ccc
       cGc cGc cGq..q.qdddddddddq.qaaaaaaaaaq.qeeeeeeeeeq.q..qGc cGc cGc
   cccccqcccqcccqq..q.qdddddddddq.qaaaaaaaaaq.qeeeeeeeeeq.q..qqcccqcccqccccc
   cGq..............q.qqqqddddddqqqqqqqqqqqqqqqeeeeeeqqqq.q..............qGc
  ccqq..............q.qllGllqF.........E.........JqllGllq.q..............qqcc
cccl....qqqqqqqqqqqqq.qqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqq.qqqqqqqqqqqqq....lccc
cGq.....qL...........N...........PqllllGllllqR...........p...........rq.....qGc
ccc.qqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqq.ccc
  c.fffffffffff.ggggggggggg.hhhhhhhhhhh.iiiiiiiiiii.jjjjjjjjjjj.kkkkkkkkkkk.c
ccc.fffffffffff.ggggggggggg.hhhhhhhhhhh.iiiiiiiiiii.jjjjjjjjjjj.kkkkkkkkkkk.ccc
cGq.fffffffffff.ggggggggggg.hhhhhhhhhhh.iiiiiiiiiii.jjjjjjjjjjj.kkkkkkkkkkk.qGc
ccc.fffffffffff.ggggggggggg.hhhhhhhhhhh.iiiiiiiiiii.jjjjjjjjjjj.kkkkkkkkkkk.ccc
  c.fffffffffff.ggggggggggg.hhhhhhhhhhh.iiiiiiiiiii.jjjjjjjjjjj.kkkkkkkkkkk.c
ccc.fffffffffff.ggggggggggg.hhhhhhhhhhh.iiiiiiiiiii.jjjjjjjjjjj.kkkkkkkkkkk.ccc
cGq.qfffffffffq.qgggggggggq.qhhhhhhhhhq.qiiiiiiiiiq.qjjjjjjjjjq.qkkkkkkkkkq.qGc
ccc.qfffffffffq.qgggggggggq.qhhhhhhhhhq.qiiiiiiiiiq.qjjjjjjjjjq.qkkkkkkkkkq.ccc
  c.qfffffffffq.qgggggggggq.qhhhhhhhhhq.qiiiiiiiiiq.qjjjjjjjjjq.qkkkkkkkkkq.c
ccc.qqqqqqqqqqq.qqqqqqqqqqq.qqqqqqqqqqq.qqqqqqqqqqq.qqqqqqqqqqq.qqqqqqqqqqq.ccc
cGq.........................................................................qGc
cccl..................................l<l..................................lccc
  ccqcccqcccqcccqcccqcccqcccqccqqqqqqqqqqqqqqqqqccqcccqcccqcccqcccqcccqcccqcc
   cGc cGc cGc cGc cGc cGc cGcvl...............lvcGc cGc cGc cGc cGc cGc cGc
   ccc ccc ccc ccc ccc ccc cccvG..M.O.Q.S.t.s..Gvccc ccc ccc ccc ccc ccc ccc
                              vl...............lv
                              vvyyyyyyyyyyyyyyyvv
                               vyyyyyyyyyyyyyyyv
                               vyyyyyyyyyyyyyyyv
                               vyyyyyyyyyyyyyyyv
                               vyyyyyyyyyyyyyyyv
                               vyyyyyyyyyyyyyyyv
                               vyyyyyyyyyyyyyyyv
                               vyyyyyyyyyyyyyyyv
                               vyyyyyyyyyyyyyyyv
                               vyyyyyyyyyyyyyyyv
                               vvyyyyyyyyyyyyyvv
                               vvyyyyyyyyyyyyyvv
                               vvyyyyyyyyyyyyyvv
                               vvvvvvvvvvvvvvvvv
ENDMAP

################################################################################
# Main vaults for 7x7 arenas. Denoted by a water pattern along the walls.
################################################################################

# Three choices between two arenas. The small arena size allows the player to
# still examine every arena before they start.
NAME:   minmay_gauntlet_77_indecision
ORIENT: encompass
TAGS:   no_item_gen no_monster_gen no_trap_gen no_pool_fixup
SUBST:  G : GT
: gauntlet_setup(_G, "DFJLNP", "EHKMOQ")
SUBVAULT: a : gauntlet_arena_77_first
SUBVAULT: p : gauntlet_arena_77_first
SUBVAULT: z : gauntlet_arena_77_first
SUBVAULT: r : gauntlet_arena_77_first
: gauntlet_arena_set_tier(2)
SUBVAULT: s : gauntlet_arena_77_second
SUBVAULT: u : gauntlet_arena_77_second
SUBVAULT: y : gauntlet_exit
: gauntlet_appearance(_G)
MAP
             c
            ccc
           ccwcc
          ccwwwcc
 ccccccccccwwwwwcccccccccc
ccwcccwccc.......cccwcccwcc
cwwwcwwwc..A.G.<..cwwwcwwwc
ccw.....D.........F.....wcc
ccc.qqqqqqqqq.qqqqqqqqq.ccc
ccw.aaaaaaaaa.ppppppppp.wcc
cww.aaaaaaaaa.ppppppppp.wwc
ccw.aaaaaaaaa.ppppppppp.wcc
ccc.aaaaaaaaa.ppppppppp.ccc
ccw.aaaaaaaaa.ppppppppp.wcc
cww.qaaaaaaaq.qpppppppq.wwc
ccw.qaaaaaaaq.qpppppppq.wcc
ccc.qqqqqqqqqqqqqqqqqqq.ccc
ccw....qE.........Hq....wcc
cww..G.q.G.G.G.G.G.q.G..wwc
ccw....qJ.........Lq....wcc
ccc.qqqqqqqqq.qqqqqqqqq.ccc
ccw.zzzzzzzzz.rrrrrrrrr.wcc
cww.zzzzzzzzz.rrrrrrrrr.wwc
ccw.zzzzzzzzz.rrrrrrrrr.wcc
ccc.zzzzzzzzz.rrrrrrrrr.ccc
ccw.zzzzzzzzz.rrrrrrrrr.wcc
cww.qzzzzzzzq.qrrrrrrrq.wwc
ccw.qzzzzzzzq.qrrrrrrrq.wcc
ccc.qqqqqqqqqqqqqqqqqqq.ccc
ccw....qK.........Mq....wcc
cww..G.q.G.G.G.G.G.q.G..wwc
ccw....qN.........Pq....wcc
ccc.qqqqqqqqq.qqqqqqqqq.ccc
ccw.sssssssss.uuuuuuuuu.wcc
cww.sssssssss.uuuuuuuuu.wwc
ccw.sssssssss.uuuuuuuuu.wcc
ccc.sssssssssquuuuuuuuu.ccc
ccw.sssssssss.uuuuuuuuu.wcc
cww.qsssssssq.quuuuuuuq.wwc
ccw.qsssssssq.quuuuuuuq.wcc
ccc.qqqqqqqqq.qqqqqqqqq.ccc
ccw.....................wcc
cwwwqwwwq..<.G.<..qwwwqwwwc
ccwqGqwqqq.......qqqwqGqwcc
 cccqqqq.qqqqqqqqq.qqqqccc
   vv.................vv
    v.G.G.G.O.Q.G.G.G.v
    v.................v
    vvyyyyyyyyyyyyyyyvv
     vyyyyyyyyyyyyyyyv
     vyyyyyyyyyyyyyyyv
     vyyyyyyyyyyyyyyyv
     vyyyyyyyyyyyyyyyv
     vyyyyyyyyyyyyyyyv
     vyyyyyyyyyyyyyyyv
     vyyyyyyyyyyyyyyyv
     vyyyyyyyyyyyyyyyv
     vyyyyyyyyyyyyyyyv
     vvyyyyyyyyyyyyyvv
     vvyyyyyyyyyyyyyvv
     vvyyyyyyyyyyyyyvv
     vvvvvvvvvvvvvvvvv
ENDMAP

NAME:   minmay_gauntlet_77_tree
ORIENT: encompass
TAGS:   no_item_gen no_monster_gen no_trap_gen no_pool_fixup
KMONS:  ' = plant
SUBST:  ' = '.
: gauntlet_setup(_G, "DFJLNPR", "EHKMOQS")
SUBVAULT: a : gauntlet_arena_77_first
SUBVAULT: p : gauntlet_arena_77_first
SUBVAULT: b : gauntlet_arena_77_first
: gauntlet_arena_set_tier(2)
SUBVAULT: r : gauntlet_arena_77_second
SUBVAULT: s : gauntlet_arena_77_second
SUBVAULT: u : gauntlet_arena_77_second
SUBVAULT: z : gauntlet_arena_77_second
SUBVAULT: y : gauntlet_exit
: gauntlet_appearance(_G)
MAP
                      ccc
                     ccwcc
                    ccwwwcc
                 ccccwwwwwcccc
                 cww...t...wwc
                 cw..A...<..wc
              cccc.....D.....cccc
              cww..qqqqqqqqq..wwc
              cw...aaaaaaaaa...wc
           cccc..t.aaaaaaaaa.t..cccc
           cww..''.aaaaaaaaa.''..wwc
           cw..t''.aaaaaaaaa.''t..wc
        cccc..''''.aaaaaaaaa.''''..cccc
        cww..t'''t.qaaaaaaaq.t'''t..wwc
        cw.........qaaaaaaaq.........wc
     cccc...qqqqqqqqqqqqqqqqqqqqqqq...cccc
     cww....qF.........E.........Jq....wwc
     cw..qqqqqqqqqqqqqqqqqqqqqqqqqqqqq..wc
  cccc...ppppppppp...........bbbbbbbbb...cccc
  cww..t.ppppppppp..'cwcwc'..bbbbbbbbb.t..wwc
  cw..''.ppppppppp.''wwwww''.bbbbbbbbb.''..wc
 cc..''t.ppppppppp.t'cwcwc't.bbbbbbbbb.t''..cc
ccw.''''.ppppppppp.''wwwww''.bbbbbbbbb.''''.wcc
cww.'''t.qpppppppq..'cwcwc'..qbbbbbbbq.t'''.wwc
ccw.''...qpppppppq...'''''...qbbbbbbbq...''.wcc
ccc.'t.qqqqqqqqqqqqq..'t'..qqqqqqqqqqqqq.t'.ccc
ccw....qL....H....Nq.......qP....K....Rq....wcc
cww.qqqqqqqqqqqqqqqqqqq.qqqqqqqqqqqqqqqqqqq.wwc
ccw.rrrrrrrrr.sssssssss.uuuuuuuuu.zzzzzzzzz.wcc
ccc.rrrrrrrrr.sssssssss.uuuuuuuuu.zzzzzzzzz.ccc
ccw.rrrrrrrrr.sssssssss.uuuuuuuuu.zzzzzzzzz.wcc
cww.rrrrrrrrr.sssssssss.uuuuuuuuu.zzzzzzzzz.wwc
ccw.rrrrrrrrr.sssssssss.uuuuuuuuu.zzzzzzzzz.wcc
ccc.qrrrrrrrq.qsssssssq.quuuuuuuq.qzzzzzzzq.ccc
ccw.qrrrrrrrq.qsssssssq.quuuuuuuq.qzzzzzzzq.wcc
cww.qqqqqqqqq.qqqqqqqqq.qqqqqqqqq.qqqqqqqqq.wwc
ccw.........................................wcc
 cc.'t'.'t'.'t'.'t'.........'t'.'t'.'t'.'t'.cc
ccw...............''...<...''...............wcc
cwwwcwwwcwwwcwwwc..t'.....'t..cwwwcwwwcwwwcwwwc
ccwcccwcccwcccwccc...........cccwcccwcccwcccwcc
 ccc ccc ccc ccccccqqqqqqqqqcccccc ccc ccc ccc
              v.................v
              v.t.t.M.O.Q.S.t.t.v
              v.................v
              vvyyyyyyyyyyyyyyyvv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vvyyyyyyyyyyyyyvv
               vvyyyyyyyyyyyyyvv
               vvyyyyyyyyyyyyyvv
               vvvvvvvvvvvvvvvvv
ENDMAP

################################################################################
# Main vaults with multiple arena shapes. Denoted by a lava pattern along the
# walls, and no fountains.
################################################################################
NAME:   minmay_gauntlet_mixed_big_or_small
ORIENT: encompass
TAGS:   no_item_gen no_monster_gen no_trap_gen
: gauntlet_setup(_G, "DFJLNP", "EHKMOQ")
SUBVAULT: a : gauntlet_arena_first
SUBVAULT: p : gauntlet_arena_77_first
SUBVAULT: b : gauntlet_arena_first
SUBVAULT: r : gauntlet_arena_77_first
: gauntlet_arena_set_tier(2)
SUBVAULT: s : gauntlet_arena_second
SUBVAULT: u : gauntlet_arena_77_second
SUBVAULT: y : gauntlet_exit
: gauntlet_appearance(_G)
MAP
  ccccccccccccccc
 cclclclclclclclcc
cc...............cc
cl.......D.....<.lc
cc..qqqqqqqqqqq..cc
cl..aaaaaaaaaaa..lc
cc..aaaaaaaaaaa..cccccccccccc
cl..aaaaaaaaaaa..lclclclclclcc
cc..aaaaaaaaaaa.A...........lc
cl..aaaaaaaaaaa......F......cc
cc..aaaaaaaaaaa..qqqqqqqqq..lc
cl..qaaaaaaaaaq..ppppppppp..cc
cc..qaaaaaaaaaq..ppppppppp..lc
cl..qaaaaaaaaaq..ppppppppp..cc
cc..qqqqqqqqqqq..ppppppppp..lc
cl......qEq......ppppppppp..cc
cc......qJq......qpppppppq..lc
cl..qqqqqqqqqqq..qpppppppq..cc
cc..bbbbbbbbbbb..qqqqqqqqq..lc
cl..bbbbbbbbbbb.....qHq.....cc
cc..bbbbbbbbbbb.....qLq.....lc
cl..bbbbbbbbbbb..qqqqqqqqq..cc
cc..bbbbbbbbbbb..rrrrrrrrr..lc
cl..bbbbbbbbbbb..rrrrrrrrr..cc
cc..qbbbbbbbbbq..rrrrrrrrr..lc
cl..qbbbbbbbbbq..rrrrrrrrr..cc
cc..qbbbbbbbbbq..rrrrrrrrr..lc
cl..qqqqqqqqqqq..qrrrrrrrq..cc
cc......qKq......qrrrrrrrq..lc
cl......qNq......qqqqqqqqq..cc
cc..qqqqqqqqqqq.....qMq.....lc
cl..sssssssssss.....qPq.....cc
cc..sssssssssss..qqqqqqqqq..lc
cl..sssssssssss..uuuuuuuuu..cc
cc..sssssssssss..uuuuuuuuu..lc
cl..sssssssssss..uuuuuuuuu..cc
cc..sssssssssss..uuuuuuuuu..lc
cl..qsssssssssq..uuuuuuuuu..cc
cc..qsssssssssq..quuuuuuuq..lc
cl..qsssssssssq..quuuuuuuq..cc
cc..qqqqqqqqqqq..qqqqqqqqq..lc
cl..............<...........cc
cc..........................lc
 cclclclclclcqqqqqqclclclclccc
  ccccccccccc.O..Q.cccccccccc
       vvvvvv......vvvvv
       v...............v
       v...............v
       vyyyyyyyyyyyyyyyv
       vyyyyyyyyyyyyyyyv
       vyyyyyyyyyyyyyyyv
       vyyyyyyyyyyyyyyyv
       vyyyyyyyyyyyyyyyv
       vyyyyyyyyyyyyyyyv
       vyyyyyyyyyyyyyyyv
       vyyyyyyyyyyyyyyyv
       vyyyyyyyyyyyyyyyv
       vyyyyyyyyyyyyyyyv
       vvyyyyyyyyyyyyyvv
       vvyyyyyyyyyyyyyvv
       vvyyyyyyyyyyyyyvv
       vvvvvvvvvvvvvvvvv
ENDMAP

NAME:   minmay_gauntlet_mixed_oops_all_seconds
ORIENT: encompass
TAGS:   no_item_gen no_monster_gen no_trap_gen
: gauntlet_setup(_G, "DFJLNP", "EHKMOQ")
: gauntlet_arena_set_tier(2)
SUBVAULT: a : gauntlet_arena_77_second
SUBVAULT: p : gauntlet_arena_second
SUBVAULT: b : gauntlet_arena_77_second
SUBVAULT: r : gauntlet_arena_second
SUBVAULT: s : gauntlet_arena_77_second
SUBVAULT: u : gauntlet_arena_second
SUBVAULT: y : gauntlet_exit
: gauntlet_appearance(_G)
MAP
                 ccccccccccccc
  cccccccccccccccclclclclclclcccccccccccccccc
 cclclclclclclclc.............clclclclclclclcc
 cl.................A..G..<.................lc
 cc.G.....G.....G......F......G.....G.....G.cc
 cl.......D.......qqqqqqqqqqq.......J.......lc
 cc...qqqqqqqqq...ppppppppppp...qqqqqqqqq...cc
 cl...aaaaaaaaa...ppppppppppp...bbbbbbbbb...lc
 cc...aaaaaaaaa...ppppppppppp...bbbbbbbbb...cc
 cl...aaaaaaaaa...ppppppppppp...bbbbbbbbb...lc
 cc.G.aaaaaaaaa.G.ppppppppppp.G.bbbbbbbbb.G.cc
 cl...aaaaaaaaa...ppppppppppp...bbbbbbbbb...lc
 cc...qaaaaaaaq...qpppppppppq...qbbbbbbbq...cc
 cl...qaaaaaaaq...qpppppppppq...qbbbbbbbq...lc
 cc...qqqqqqqqq...qpppppppppq...qqqqqqqqq...cc
 cl.....q.E.q.....qqqqqqqqqqq.....q.K.q.....lc
 cc.G...q.G.q...G....q.H.q....G...q.G.q...G.cc
cc......q.L.q........q...q........q.P.q......cc
cl...qqqqqqqqqqq.....q.N.q.....qqqqqqqqqqq...lc
cc...rrrrrrrrrrr...qqqqqqqqq...uuuuuuuuuuu...cc
cl...rrrrrrrrrrr...sssssssss...uuuuuuuuuuu...lc
cc...rrrrrrrrrrr...sssssssss...uuuuuuuuuuu...cc
cl...rrrrrrrrrrr...sssssssss...uuuuuuuuuuu...lc
cc.G.rrrrrrrrrrr.G.sssssssss.G.uuuuuuuuuuu.G.cc
cl...rrrrrrrrrrr...sssssssss...uuuuuuuuuuu...lc
cc...qrrrrrrrrrq...qsssssssq...quuuuuuuuuq...cc
cl...qrrrrrrrrrq...qsssssssq...quuuuuuuuuq...lc
cc...qrrrrrrrrrq...qqqqqqqqq...quuuuuuuuuq...cc
cl...qqqqqqqqqqq...............qqqqqqqqqqq...lc
cc...............G.....G.....G...............cc
cl.G......G.........................G......G.lc
cc...............clcqqqqqqqclc...............cc
 cclclclclclclclcccc.......cccclclclclclclclcc
  cccccccccccccccvv..G.G.G..vvccccccccccccccc
                vv...M.O.Q...vv
               vv.............vv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vyyyyyyyyyyyyyyyv
               vvyyyyyyyyyyyyyvv
               vvyyyyyyyyyyyyyvv
               vvyyyyyyyyyyyyyvv
               vvvvvvvvvvvvvvvvv
ENDMAP