##############################################################################
#
# The Sewer (Eino)
#
# The Sewer aspires to be an exciting place to go with a low level character.
#
# Flavour: drainage and sewer themed, so rats, kobolds, etc. Water is (dark
# and light) green.
# Gameplay: an early challenge, featuring monsters which are bland later but
# interesting when encountered at low level (sewer rat, bullfrog etc.) Loot
# consists of renewable items; the harder maps contain a few good items.
#
##############################################################################

{{

function sewer_portal(e)
  e.tags("no_monster_gen")
  e.kfeat("1 = W")
  e.colour("1W = lightgreen / cyan w:5")
  e.tile("} = dngn_portal_sewer")
  e.ftile("}` = floor_iron")
  e.tile("v = dngn_metal_wall_green")
  e.colour("wv = green")
  e.tags("sewer_entrance")
end

function sewer_setup(e)
  e.kfeat("< = exit_sewer")
  e.tile("v = dngn_metal_wall_green")
  e.colour("wv = green")
  e.colour("W = lightgreen")
  e.kfeat("E = endless_sludge")
end
}}

# Basic sewer entries ##########################################
# NOTE: See comments above regarding KMONS for the 1 glyph in these vaults.
default-depth: Sewer

NAME:   portal_sewer_entry_a
: sewer_portal(_G)
SUBST:  1 = 2234..........
MONS:   nothing
MONS:   rat / sewer rat w:5
MONS:   ball python
MONS:   giant cockroach / worm w:3
MAP
.........
...111...
..1WWW1..
.1WWWWW1.
.1WW}WW1.
.1WWWWW1.
..1WWW1..
...111...
.........
ENDMAP

NAME:   portal_sewer_entry_b
ORIENT: float
: sewer_portal(_G)
SUBST:  ' = W. , W = WW1
KMONS:  1 = rat w:20 / sewer rat / ball python / \
            giant cockroach / worm w:3
MAP
xxxxxx
x}WWW'
xWWWW.
xWWW'.
xWWW..
xW'...
ENDMAP

NAME:   portal_sewer_entry_c
WEIGHT: 4
: sewer_portal(_G)
NSUBST: c = 1:+ / 2:n / *:c
NSUBST: W = 8 = 111W / *:W
KMONS:  1 = rat w:20 / sewer rat / ball python / \
            giant cockroach / worm w:3
MAP
.........
.ccccccc.
.cWWWWWc.
.cWW}WWc.
.cWWWWWc.
.cWWWWWc.
.ccccccc.
.........
ENDMAP

NAME:   portal_sewer_entry_d
: sewer_portal(_G)
KMONS:  1 = patrolling sewer rat
KFEAT:  1 = W
MAP
vvvvvvvvvv
vW}W1WWWWW@
vvvvvvvvvv
ENDMAP

NAME:   portal_sewer_entry_e
: sewer_portal(_G)
ORIENT: northwest
NSUBST: X = 2:+ / *:x
SUBST:  w = WW.
NSUBST: W = 8:111W / *:W
KMONS:  1 = rat w:20 / sewer rat / ball python / \
            giant cockroach / worm w:3
MAP
xxxxxxxxx
xWWWXWww.
xW}WXWww.
xWWWXWww.
xXXXXWw..
xWWWWWw..
xwwwww...
xwww.....
x........
ENDMAP

NAME:   portal_sewer_entry_f
KMONS:  1 = leopard gecko / giant cockroach / nothing w:5
NSUBST: W = 6:1 / *:W
SUBST:  x = v.y
CLEAR:  y
COLOUR: W = green / cyan w:5
: sewer_portal(_G)
MAP
vxvxvxvxvxv
.WWWWWWWWW.
@WWWW}WWWW@
.WWWWWWWWW.
vxvxvxvxvxv
ENDMAP

NAME:   gup_sewer_entry_pipe
TAGS:   patrolling no_pool_fixup
: sewer_portal(_G)
KMONS:  2 = giant cockroach / leopard gecko / nothing
SUBST:  W = WW.
MAP
@Wxxxxxxxxx
WWWWWWWWWWxx
xWWwwwwwwWWx
xxxxxxxxx22x
 xxwwwwww22x
xxWWW2}2WWxx
x22wwwwwwxx
x22xxxxxxxxx
xWWwwwwwwWWx
xxWWWWWWWWWW
 xxxxxxxxWW@
ENDMAP

NAME:   gup_sewer_entry_spiral
TAGS:   patrolling
: sewer_portal(_G)
KMONS:  1 = rat / sewer rat w:5 / giant cockroach / \
            leopard gecko / worm w:5 / nothing
NSUBST: - : 8:1 / 8:. / *:W
MAP
xxxxxxxxxx
x--------xx
@xxxxxxxx-x
xx-----xx-x
x-xxxxx-x-x
x-xx-}x-x-x
x-x-xxx-x-x
x-xx----x-x
x-xxxxxxx-x
xx-------xx
 xxxxxxxxx
ENDMAP

NAME:   gup_sewer_entry_bubbles
TAGS:   patrolling
: sewer_portal(_G)
KMONS:  1 = rat / sewer rat w:5 / giant cockroach / \
            leopard gecko / worm w:5 / nothing
NSUBST: - = 8:1 / *:W
MAP
 xxx    xxx
xx-vvvvvv-xx
x----------x
x---vvvv---x
xv-vv--vv-vx
 v-v--}-v-v
 v-v----v-v
xv-vv--vv-vx
x---v--v---x
x---v------x
xx-xxxxxx-xx
 x@x    xxx
ENDMAP

NAME:   gup_sewer_entry_bowl
: sewer_portal(_G)
KMONS:  1 = rat / sewer rat w:5 / \
            giant cockroach / leopard gecko / worm w:3 / nothing
MAP
..........
.xxx.xxxx..
.xWxxxWWxx+.
.xW+WW11WWx.
.x}+WW11WWx.
.xWxxxWWxx+.
.xxx.xxxx..
..........
ENDMAP

NAME:   gup_sewer_entry_glass
: sewer_portal(_G)
KMONS:  1 = adder / giant cockroach / nothing
MAP
..........
.mmmmmmm.
.mW1W1Wm.
.+W1}1Wm.
.mW1W1Wm.
.mmmmmmm.
.........
ENDMAP

NAME: lightli_sewer_entry_pipes
NSUBST: o = 1:} / *:W
SUBST: W = WWWWW1
KMONS: 1 = sewer rat / rat / worm
: sewer_portal(_G)
MAP
vvvvvvvvvv
vovWovWvvv
vWvWvvWWWv
vWvWvvWvWv
vWvWWvWvWv
vWvvWvWvWv
vWWWWvWvWv
vvvvWvWvWv
vWWWWWWvov
vWvvvvvvvv
vWWWWWWWWW
vvvvWvWvWW
..WWWWW.W.
....W.....
..........
ENDMAP

NAME: cheibrodos_sewer_entry_bend
ORIENT: northeast
KMONS: 1 = worm
NSUBST: - = 1:1 / 1=1.. / 1=1...
KFEAT: A = iron_grate
: sewer_portal(_G)
MAP
xxxxxxxxxxxxxx
xxAAxxxxxxxxxx
xxWWxxxxx---xx
...WWWWWWW--xx
....WWWWWWW-xx
 @..vvvv}WWxxx
  .....mvWWxxx
     @..vWWxxx
      @.vWWxxx
       .vWWxxx
       ...WWAx
       ....WAx
        @..xxx
         ..xxx
ENDMAP

NAME: cheibrodos_sewer_entry_drain
TAGS: no_monster_gen
ORIENT: float
KMONS: 1 = rat / ball python / giant cockroach / worm
NSUBST: 1 = 2:1 / 2=1.. / *:.
: sewer_portal(_G)
MAP
  ...@...
 ..x1.1x..
...WWWWW...
.xWWWWWWWx.
.1WWWWWWW1.
@.WWW}WWW.@
.1WWWWWWW1.
.xWWWWWWWx.
...WWWWW...
 ..x1.1x..
  ...@...
ENDMAP

NAME: cheibrodos_sewer_entry_sluice
TAGS: no_monster_gen
ORIENT: float
KMONS: 1 = ball python / worm / bat / w:5 sewer rat
SHUFFLE: AB
SUBST: A = w, B = .
NSUBST: . = 2:1 / 2=1.
: sewer_portal(_G)
MAP
.....@.....
.xxwwAwwxx.
.xxwwAwwxx.
.ww.....ww.
.ww.```.ww.
.ww.`}`...@
.ww.```.ww.
.ww.....ww.
.xxwwwwwxx.
.xxwwwwwxx.
...........
ENDMAP

# The actual portal vaults #####################################
default-depth: Sewer

### KOBOLDS ################################################################
# Sewers with kobold settlements. Combined weight 50.

NAME:     disused_pipe
TAGS:     no_monster_gen no_pool_fixup no_exits sewer_edge
ORIENT:   northeast
: sewer_setup(_G)
MONS:     sewer rat band / kobold band / robin band
KMONS:    q = crocodile / slime creature
KFEAT:    q = deep_water
COLOUR:   q = green
TILE:     q = dngn_deep_water_murky
MAP
vvvvvvvvvvvvvvvvvvvvvvvvv
vwqwWWW..}..1..{..WWWwqwv
vvvvvvvvvvvvvvvvvvvvvvvvv
ENDMAP

############################################################################
# Sewer with a small kobold camp, potion treasure and Ely/Zin altar.
#
NAME:     elyvilon_niche
TAGS:     no_pool_fixup sewer_cave
ORIENT:   float
: sewer_setup(_G)
SUBST:    ? = .x
# some fresh water for the kobolds
KFEAT:    y = shallow_water
TILE:     y = dngn_shallow_water
COLOUR:   y = cyan
KFEAT:    F = altar_zin / altar_elyvilon w:30
MONS:     jackal band, big kobold band, slime creature
ITEM:     potion of curing / potion of heal wounds
ITEM:     any scroll
SHUFFLE:  de
MAP
xxxxxxxxxx@xxxxx
xedFxxxx.12....xx
xdexxxx......y..x
xx?3?xxx..2.yyy.x
 xx?.xxx?....y.xx
  x?x..?.?....xx
  xx???.xxxxxxx
   xxxxxx
ENDMAP

############################################################################
# Water treatment center, staffed by kobolds
#
NAME:   sewer_minmay_treatment
TAGS:   sewer_edge
ORIENT: west
MONS:   big kobold band / gnoll sergeant band w:5 / gnoll shaman band w:5 / robin band
ITEM:   potion of curing / potion of heal wounds / potion of might w:4 /\
        potion of brilliance w:4 / potion of agility w:4 /\
        any scroll w:7 / any armour w:2 / any weapon w:2
: sewer_setup(_G)
: set_feature_name("clear_stone_wall", "large window")
MAP
Ewvvvvvvv
Ewnddv..v
Ewnd.1..v
Ewvv....+@
Ewnd.1..v
Ewnddv..v
Ewvvvvvvv
ENDMAP

### FROGS ##################################################################
# Giant frogs, specifically. Combined weight 40.

############################################################################
# Two fairly watery vaults with (usually) two bullfrogs in the end.
# Loot are good items, and often a scroll of blinking.
#
NAME:     sewer_frog_pond
TAGS:     no_pool_fixup sewer_cave
SUBST:    - = WWW.
ITEM:     scroll of teleportation w:2 / scroll of blinking
MONS:     generate_awake cinder newt / generate_awake adder
NSUBST:   ! = 4:! / *:w
NSUBST:   w = 8:1 / *:w
KFEAT:    1! = w
COLOUR:   1! = green
KMONS:    ! = spiny frog / prince ribbit / crocodile
: sewer_setup(_G)
MAP
c.@@@1cccccccccccccc
c.WwwWccccccc!!!!!!cccc
c.WwwWccccwwwwwwwww!!!cc
c-WwwWccwwwWwwwwwwwwww!cc
c-WwwWcwwWW<-wwwwwwwwww!cc
c-WwwwWwww-..-ww.--wwwww!c
c-WwWwwWww---.-.*.--WWwwwc
c-WwWWWwWww-.--***.wwwWwwc
c-WwWWwwwWww.---d-wwwwwwwc
c-WwWWWwwwW------wwwwwwwwc
c-WwWWWwwwwW---wwwwwwWwwcc
c-WwW.WwwwwwwwwwwwWwWwwwcc
c.WwW.WwwwwwWWwwwwwwwWccc
c.@@@W..WWWWwwWwWWccccc
       cccccccccccc
ENDMAP

NAME:     sewer_frog_spiral
TAGS:     no_pool_fixup sewer_cave
NSUBST:   ! = 4:! / *:w
SUBST:    - = WWWW.
KMONS:    W = cinder newt / giant cockroach w:30 / \
              sewer rat w:70 / slime creature / adder / \
			  ooze w:40 / nothing w:130
KFEAT:    W = W
KFEAT:    ! = w
COLOUR:   ! = green
KMONS:    ! = spiny frog / prince ribbit / crocodile
SHUFFLE:  edfgh$*
ITEM:     ration, any ring
ITEM:     scroll of teleportation w:2 / scroll of blinking
ITEM:     any potion, any ring
KITEM:    de = *
: sewer_setup(_G)
MAP
         ccccccc
      cccc-----cccc
    ccc-----------ccc
   cc-----ccccc-----cc
  cc----ccc!!!ccc----cc
ccc----cc!w-ww!!cc----c
c-----ccw--..-ww!cc---cc
c{----cw--.gh*.-w!c----c
c-----c--.edf.---!c----c
ccc---c---.$---w!cc---cc
 cc---c--.----w!cc----c
  c---cc--c-wwccc----cc
  cc---c--ccccc-----cc
   @@@@cc-..------ccc
        cc.----cccc
         cc-}-ccc
          ccccc
ENDMAP

############################################################################
# Sewer in the shape of a ubend. Frogs at the end.
#
# Harder than the two other frog maps. Frogs are guaranteed, and neither
# exit nor chokepoints are reachable when one sees them.
#
NAME:    sewer_minmay_ubend
TAGS:    no_pool_fixup sewer_special
ORIENT:  southeast
SHUFFLE: de
MONS:    ooze w:2 / sewer rat
MONS:    slime creature w:3 / adder w:5 / ooze w:1
MONS:    bullfrog / spiny frog / prince ribbit / crocodile
ITEM:    potion of curing / potion of heal wounds / potion of might /\
         potion of magic / potion of haste w:5 / potion of brilliance w:5
ITEM:    scroll of teleportation / scroll of blinking w:5 /\
         scroll of fear w:5 / scroll of magic mapping / scroll of fog
SUBST:   y : yyyyyyW., Y : YYYYYYWw, z = x., Z = wwwxx
SUBST:   y = W., Y = Ww
SUBST:   c = c x:1
NSUBST:  v = 3:W. / *:c
NSUBST:  V = 3:W. / *:c
KMONS:   Ww = cinder newt / withered plant w:30 / nothing w:260
KFEAT:   w = deep_water
KFEAT:   W = shallow_water
SUBST:   c : vc
: sewer_setup(_G)
MAP                
@@@@@@@@cc   ccccccccccc 
@........cc  cdedcc3..3cc
@......2..c  ce<e+2..3.3c
c+cc..2.2.cc cdedc.2..2.c
cddcc..2...c ccccc2.2..2c
cddcczyyyyzc     czyyyyzc
cccccYYYYWYc     VYYZWYYc
    cwwwYWYv     VYYWYwYc
    cwwYYWYv     VwYWYYwc
    cwYYWYYv     VwYWWYwc
    cYWWYYwv     VwYYWZYc
    cWYYYwwv     VwwYZWYc
    .ZyyyyZv     VwwwY.Y.
    .yy%%ywv     VwwYZWY.
    .ZyyyyZv     VwwYWZY.
    cwwYYWWv     VwYYWYwc
    cwwwYYWv     VwYWWYwc
    cwYWYYwv     VwYWYwYc
    cYYWYYYc cccccYYWYYYc
    czyyyyzc ceecczyyyyzc
    c1.1.1.ccceecc......c
    cc1.1.1.cc+cc2.....cc
     c.1.1.1.c.........c 
     cc.1.1.1.........cc 
      cccc.2......2cccc  
         cccc2.2cccc     
            ccccc        
ENDMAP

## BIG BADDIE (Purgy) ######################################################
# Threat is mostly a single larger creature, often Purgy. Combined weight 40.

NAME:     purgy_house
TAGS:     no_item_gen no_monster_gen no_pool_fixup sewer_edge
ORIENT:   north
KMONS:    2 = sewer rat band / giant frog / hound
KMONS:    1 = Purgy
KITEM:    d = potion of berserk rage / potion of haste w:5
KFEAT:    d = shallow_water
SUBST:    ` = WW.
SUBST:    c : cv
: sewer_setup(_G)
MAP
 ccccccc
cc$***$cc
c%.$*$.%c
c...1...c
c.2.w.2.c
c..wWw..c
c.WwWwW.c
cc.WwW.cc
 cccWccc
   cdc
   c`c
   c`c
   c`c
   c`c
   c`c
   c`c
   c@c
ENDMAP

###############################################################################
# Another one with a big bad monster.
#
NAME:   sewer_the_crocodile
TAGS:   no_item_gen no_monster_gen no_pool_fixup
ORIENT: encompass
SUBST:  - = WW...
MONS:   kobold, jackal
SUBST:  3 = 334
KMONS:  3 = crocodile
KMONS:  4 = Purgy, crocodile
ITEM:   potion of agility / potion of heal wounds / \
        potion of invisibility / potion of mutation / potion of degeneration /\
        scroll of blinking / scroll of teleportation / scroll of fear / \
        scroll of random uselessness / ration / potion of blood / \
        nothing w:140
: sewer_setup(_G)
MAP
   ccccccccc
   c%*c3c*%c
   c*-c-c-*c
   ccc---ccc
    cc---cc
    c-----c
   cc.---.cc
   c..-c-..c
  cc.-ccc-.cc
  c.--ccc--.c
  c--ccccc--c
  c.-ccccc-.c
  c.-ccccc-.c
  c--ccccc--c
  c.--ccc--.c
  cc--ccc--cc
   c.--c--.c
   cc-----cc
    c..-..c
    cc---cc
     c.-.c
     cc-cc
     cc-cc
   cccc-cccc
cccc..---..ccccc
cWWWWWWWWWWWWWWc
cccc.......ccccc
  ccc..c..cccxxxx
  c....c.21.cdddx
  c.A<.c2....dddx
  c....c1.1.cxxxx
  ccccccccccc
ENDMAP

############################################################################
# Purgy island. by co
# Purgy lives on this island, he appears in one of three places, if he is
# outside the loot room, you can get the loot without fighting him
# (if you are sneaky enough). Not much to walk on here, if you manage to
# confuse Purge he will probably drown. Some stone walls from the secret room
# can be seen from the island.
#
NAME:    sewer_co_purgy_island
TAGS:    no_item_gen no_monster_gen no_pool_fixup
ORIENT:  encompass
#      Placing Purgy, he appears in one of three places. The crocodile serves
#      as the back-up, in case Purgy was already placed in the Dungeon.
SHUFFLE: PQR
KMONS:   P = Purgy, crocodile
SUBST:   Q = .
SUBST:   R = .
#      lesser monsters.
KFEAT:   1 = w
COLOUR:  1 = green
KMONS:   1 = cinder newt / ball python w:5 / \
         generate_awake cinder newt / generate_awake ball python w:5 / \
         adder w:4 / nothing w:6
#      loot
SUBST:   e = !!!!????%
KITEM:   ? =  potion of curing /\
         potion of heal wounds / potion of might / potion of magic /\
         potion of haste w:5 / potion of brilliance w:5
KITEM:   ! =  scroll of teleportation / scroll of fear w:5 /\
         scroll of blinking w:3 / scroll of magic mapping w:3 /\
         scroll of fog w:3
#      Randomized environment
SUBST:   . = .WW
SUBST:   X = xx.
SUBST:   Y = xw
: sewer_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxcccxxxx
xxxxxxxxxxxxxxYYxxxYYYxxxxYYYxxxxxxcc.ccxxx
xxxxxxxxxxxxxYwwYYwwwwwYYwwwwccccccc.e.ccxx
xxxxxxxxxxYwwwwwwwwwwwwwwwwwwwwwww..*<*.cxx
xxxxxxxxxxYwwwwwwwwwwwwwwwwwwccccccc.e.ccxx
xxxxxxxxYYwwwwwwwwwwwwwwwwwwwwYxxxxcc.ccxxx
xxxxxxxYwwwwwwwwwwwwwwwwwww1wwwYxxxxcccxxxx
xxxxxxYwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxxxx
xxxxxxwwwww1wwwww....wwwwwwwwwwwwYxxxxxxxxx
xxxxxYwwwwwwwww..XXXX..wwwwwwwwwwwYxxxxxxxx
xxxxxwww1wwwww.XXxxxxXXQ.wwwwwww1wwwYxxxxxx
xxxxYwwwwwww...XxxxxxxxxX..wwwwwwwwwwxxxxxx
xxxxYwwwwww.XXXxxxxxxxxxxxX..wwwwwwwwYxxxxx
xxxYwwwwww.XxxxxxxxxxxxxxxxxX.wwwwwwwwYxxxx
xxxYwwwww.XxxxxxxxxxxxxxxxxxxX.wwwwwwwwYxxx
xxxxYwwww.XxxxxxxxxxxxxxxxxxxX.wwwwwwwwYxxx
xxxxYwwwww.XxxxxxxxcccccxxxxxX.wwwwwwwYxxxx
xxxYwwwwww.Xxxxxxxxceeeccccccc.wwwwwwwYxxxx
xxxYwwwwwww.Xxxxxxxce<....P...wwwwwwwwYxxxx
xxYwwwwwwwwwAXxxxxxceeeccccccc.wwwwwwwYxxxx
xxYwwww1wwww<XxxxxxcccccxxxxxX.wwwwwwwYxxxx
xxxYwwwwwww.XxxxxxxxxxxxxxxxxxX.wwwwwwYxxxx
xxxxwwwwww.XxxxxxxxxxxxxxxxxxxX.wwwwwwYxxxx
xxxxYwwwwww.XXxxxxxxxxxxxxxxxX.wwwwwwwwYxxx
xxxYwwwwwwww..XXxxxxxxxxxxxxX.wwwwwwwwYxxxx
xxxYwwwwwwww..XXxxxxxxxxxxXXX.wwwwwwwwYxxxx
xxxxwwwwwwwwww..XxxxxxxxxX...wwwwwwwwwwYxxx
xxxxYwwww1wwwwww.XxxxxxXX.wwwwwwwwwwwwwYxxx
xxxxxwwwwwwwwwww.XxxxxXR.wwwwwwwwwwwwwYxxxx
xxxxYwwwwwwwwwwww.XXXX.wwwwwww1wwwwwYYxxxxx
xxxxxYwwwwwwwwwwww....wwwwwwwwwwwwwYxxxxxxx
xxxxxxYwwwwwwwwwwwwwwwwwwwwwwwwwwwYxxxxxxxx
xxxxxxYwwwwwwwwwwwwwwwwww1wwwwwwwYxxxxxxxxx
xxxxxxYwwwwwwwwwwwwwwwwwwwwwwwwYYxxxxxxxxxx
xxxxxxxYwwwwwwwwwwwwwwwwwwwwwwYxxxxxxxxxxxx
xxxxxxxYwwwwwwwwwwwwwwwwwwwwwYxxxxxxxxxxxxx
xxxxxxxxYxYwwwwwwYYwwwwwwwYYwYxxxxxxxxxxxxx
xxxxxxxxxxxxYYYYxxxYYYxYYYxxYYxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   kennysheep_door_sewer
TAGS:   no_item_gen no_monster_gen no_pool_fixup
ORIENT: encompass
MONS:   kobold w:40 / bat / cinder newt / giant cockroach / \
        adder w:5 / big kobold w:1
KMONS:  2 = purgy, crocodile
ITEM:   potion of agility / potion of heal wounds / \
        potion of invisibility / potion of mutation / potion of degeneration / \
        scroll of blinking / scroll of teleportation / scroll of fear / \
        scroll of random uselessness / ration / potion of blood / \
        nothing w:140
NSUBST: e = 3:* / 9:d / *:.
NSUBST: 0 = 5:1 / 1:2 / *:.
KFEAT:  ' = deep_water
COLOUR: ' = green
KFEAT:  " = shallow_water
COLOUR: " = lightgreen
KPROP:  "' = no_tele_into
: sewer_setup(_G)
MAP
        ccccccccccccccccccccc
        cecceccecceccecceccec
        c0cc0cc0cc0cc0cc0cc0c
        c+cc+cc+cc+cc+cc+cc+c
ccccccccc...................ccccccccc
c"""""""nWWWWWWWWW.WWWWWWWWWn"""""""c
c'''''''nwwwwwwwwwAwwwwwwwwwn'''''''c
c"""""""nWWWWWWWWW.WWWWWWWWWn"""""""c
ccccccccc...................ccccccccc
        c+cc+cc+cc.cc+cc+cc+c
        c0cc0cc0cc.cc0cc0cc0c
        cecceccecc<ccecceccec
        ccccccccccccccccccccc
ENDMAP

### OTHER #####################################################################
# Other sorts of Sewer maps, most weighted 10. (Total 68).

###############################################################################
# If you can get to the middle, there are four doors to try; two will give you
# monsters, two will yield you treasure.
#
NAME:     sewer_fruit_machine
TAGS:     no_item_gen no_monster_gen no_pool_fixup
ORIENT:   encompass
SHUFFLE:  QEDS
SUBST:    Q = 4, E = 5, D : fgh, S = $
NSUBST:   f = 1:f / *:'
NSUBST:   g = 2:g / *:'
NSUBST:   h = 3:h / *:'
#       some random monsters I: bat or nothing
NSUBST:   w = 4:" / *:w
KFEAT:    " = w
KMONS:    " = bat / nothing
COLOUR:   " = green
#       some random monsters II: standard Sewers set
NSUBST:   W = 4:? / *:W
KFEAT:    ? = W
KMONS:    ? = bat / cinder newt / ball python / giant cockroach / \
              leopard gecko / ooze w:4 / worm w:4 / adder w:2
COLOUR:   ? = lightgreen
#       a little more random shallow water
SUBST:    - = W..
: sewer_setup(_G)
MONS:     bat / cinder newt / ball python / giant cockroach / \
          leopard gecko / ooze w:4 / worm w:4 / adder w:2
SHUFFLE:  23
MONS:     bat / nothing w:2
MONS:     patrolling jackal
MONS:     worm / nothing w:3
MONS:     ooze / nothing w:2
#       chances are good you'll find something to cross the deep water
SHUFFLE:  de
ITEM:     scroll of fear / scroll of fog / scroll of teleportation w:50 / \
          scroll of blinking / nothing
ITEM:     potion of invisibility / \
          potion of might w:3 / potion of agility w:4 / \
          potion of brilliance w:3 / nothing
#       possibly rather generous loot, if you get to the center
ITEM:     scroll of vulnerability / hat / \
          potion of magic / potion of brilliance / potion of resistance
ITEM:     potion of invisibility / pair of gloves w:5 / \
          pair of boots w:5 / potion of agility / nothing
ITEM:     potion of resistance / potion of might / \
          potion of berserk rage / buckler / nothing
KITEM:    ' = scroll of random uselessness w:130 / \
              scroll of teleportation / \
              potion of mutation / \
              hat / buckler / pair of gloves w:5 / \
              pair of boots w:5
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xwwwwwwwwwwwWWW-WW-W--WWWWW-WWW--W--WW-WW-WW-W-WWwwwwwwwwwwwx
xwwwwwwwwwww-wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww-wwwwwwwwwwwx
xwwwwwwwwwwwWwWW----WWWWWWWW-W-WWW-W-WW-WW-W-WWwWwwwwwwwwwwwx
xxxxxxxxxxxxWwWxxxxxxxxxxxxxxxxxxxxxxxWwWxxxxxWwWxxxxxxxxxxxx
           xWwWxxxxxxxxxxxxxxxxxxxxxxxWwWxxxxxWwWxxxxxxxx.1.xx
           x-wWxxxxQQQQxxxDDDDxxxxxxxxWwWxxxxx-wWxxxx1xx....1xx
           xWwWxxxxx++xxxxx++xxxxxxxxx-wWxxxxxWwW--W-.........x
           x-wWW-W..............<xxxxxWw-xxxxxWwWWW-1....A...1x
           x-wWWW--.............<xxxxxWwWxxxxxWwWW-.xxxx......x
           xWwWxxxxx++xxxxx++xxxxxxxxxWwWxxxxx-wWxxxxxxxxx.<xxx
           x-wWxxxxEEEExxxSSSSxxxxxxxx-w-xxxxxWw--xxxxxxxxxxx
   xxxxxxx x-wWxxxxxxxxxxxxxxxxxxxxxxxWwWxxxxxWw-xxxxxxxxxxxx
 xxx.d..dxxxWwWxxxxxxxxxxxxxxxxxxxxxxxWwWxxxxx-wWxxxxxxxxx.exx
xx.3...d.dxxWwWWWWWW---WW-W-WW-W-WWWW--wWxxxxxWwWxxxxxxxx-22ex
x.3....dxxxx-w-wwwwwwwwwwwwwwwwwwwwwww-w-xxxxxWwWWWWWWWW---2.ex
xx3...xxxxxxWwWW-W-WWW-W-W-WWWWW-WWWWWWw-xxxxxWw-wwwwwwww---2.x
 x3-...-xxxx-w-xxxxxxxxxxxxxxxxxxxxxxxWwWxxxxx-wWWWWWWW-----..x
 xxx------xxWwWxxxxxxxxxxxxxxxxxxxxxxxWwWxxxxxWwWxxxxxxxxx-.exx
xxxxxx------WwWxxxxxxxxxxxxxxxxxxxxxxxWwWxxxxxWwWxxxxxxxxxxxxx
xwwwwww--WWwwwWWWW-W-W-W-W-WWWW-W-W-W-W-WW---WWwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

###############################################################################
# A sewer opening to sea by st_.
#
NAME:   sewer_sea
TAGS:   no_item_gen no_monster_gen no_rotate no_pool_fixup
ORIENT: encompass
WEIGHT: 5
MONS:   rat w:4 / nothing w:1, rat / sewer rat w:5
KMONS:  G = merfolk siren hd:3 name:sickly name_adjective name_species
KMONS:  H = merfolk avatar hd:3 name:malarious name_adjective name_species
KMONS:  hJ = cinder newt / ball python / adder / nothing w:2
KMONS:  g = cinder newt / ball python / giant cockroach / \
            leopard gecko / nothing w:7
KFEAT:  gsh = W
KFEAT:  O = open_sea
COLOUR: gh = lightgreen
COLOUR: HJ = blue
KFEAT:  GnHJ = w
COLOUR: G = green
COLOUR: s = cyan
KITEM:  M = scroll of random uselessness q:4
KITEM:  N = scroll of teleportation q:2 ident:type
: sewer_setup(_G)
MAP
OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO                        cccc       cccccccc
OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO                       ccW.cc      cWW1..1c
OOOnnnnnnnnnnnnnnnnnnnOOOOOOOOOOOOOO                     cccWWW.ccccccccW..c..c
OOOnnnnnnnnnnnnnJnnnnnOOOOOOOOOOOOOO                     c<AWWWW...1.2...1.cc.c
OOOnnnnnnnnnnnnnnnnnnnOOOOOOOOOOOOOO                     cccccccccccccccccccc.c
OOOnnnnnnnnnJnnnscccccccccccccc  cccccccccccccccc ccccccccccwhwwwwwwwWwwwww2..c
OOOnnnnnnnnnnnns.+.....+...%Wwc  c1.....cc$$$.1McccwwWWWgWWWWWWWW..W..WW.Www..c
OOOnnnnnnnnHnnsW.ccccccc...%*Wc  c.ccccc.c.1.W.NcccwWWccccccccccccc11cccc.gwWwcc
OOOnnnnnnnnnnnsW.ccc.1cc...%*Wc  c2cwwcc.ccccWcccwwwc.c           cccc  cc..WWwc
OOOnnnnnnnnnJnsW.ccc..cc...*WWc ccwWgcwc.cGgwWWWWWWcc.c                  ccccwwc
OOOnnnnnnnnnnssWWccc1.cc<..WwccccwgWccwc.chwgWW..1....c            cccccccccccwc
OOOnnnnnnnnsssWWWWWc.1ccccccccwwwWW.1cwWWcWWW..2ccccccc            cGwwwwwwwwwwc
OOOnnnnnnnsssWWWWWWWWsswwswwwhcWW..1ccccWWWW1..cc                  ccccccccccccc
OOOnnnnnnnnssshWWWWc..cccccccccW.1.cc  ccW1..ccc
OOOnnnnnnnnnnssWgccc.2c  cwg2....ccc    cccccc
OOOnnnnnnnnnnnssWcccc1cc cgWcccccc
OOOnnnnnnnnnnnnsccccc..ccc.1c
OOOnnnnnnnnnnnnnsccccc1.1..cc
OOOnnnnnnnnnnnnnnnnnnccccccc
OOOnnnnnnnnnnnnnnnnnnnOOOOOOOOOOOOOO
OOOnnnnnnnnnnnnnnnnnnnOOOOOOOOOOOOOO
OOOnnnnnnnnnnnnnnnnnnnOOOOOOOOOOOOOO
OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
ENDMAP

############################################################################
# 15 rooms, by co
# All rooms will always be accessible, except the room in the center, which
# is surrounded by deep water.
#
NAME:    sewer_co_15_rooms
TAGS:    no_item_gen no_monster_gen no_pool_fixup
ORIENT:  encompass
MONS:    kobold / w:7 big kobold
MONS:    kobold / adder / ball python / w:7 worm / w:7 ooze / \
         rat / sewer rat w:5
SUBST:   . = W..
SUBST:   X = xx.
SUBST:   2 = 222..
#      generate path to center
SHUFFLE: BC/DE/GF/IH
SUBST:   B = w, C = W
SUBST:   DGI = c, EFH = x
#      generate path to the first cave.
SHUFFLE: JK/LM
SUBST:   JK = .
SUBST:   M = x, L = c
#      generate path to the second cave.
SHUFFLE: PN/QR
SUBST:   PN = .
SUBST:   Q = x, R = c
#      generate a paths between the big rooms.
SHUFFLE: yZzSsUuTgY
SUBST:   y = w
SUBST:   hVZzSsUuTgY = W
#      loot
SUBST:   e = !!!!?????
KITEM:   ? = scroll of random uselessness w:5 /\
         potion of curing / potion of heal wounds / potion of might /\
         potion of magic / potion of haste w:5 / potion of brilliance w:5
KITEM:   ! =  scroll of teleportation / scroll of fear w:5 /\
         scroll of blinking w:3 / scroll of magic mapping w:3 /\
         scroll of fog w:3
: sewer_setup(_G)
MAP
xxxxxxxxxxxxcccxxxxxxxcccxxxxxxxxxxxx
xxxxxxxxxxxcccccxxxxxcccccxxxxxxxxxxx
xxxxxxxxxxcc...ccxxxcc...ccxxxxXXXxxx
xxxxxxxxxxc.2...ccccc...2.cxxxx.1.Xxx
xxxxxxxxxxc..2..yyyyy..2..NPPP.e.1Xxx
xxxxxxxxxxc...2.ccccc.2...cxxxX.e.Xxx
xxxxxxxxxxcc...ccxxxcc...ccxxxxX.xxxx
xxxxxxxxxxxccZccxxxxxcczccxxxxxxQxxxx
xxxxxxxxxxxxcZcxxxxxxxczcxxxxxxxQxxxx
xxxxxxxxxxxxcZcxxxxxxxczcxxxxxxxQxxxx
xxcccccxxxxccZccxxxxxcczccxxxxccRccxx
xcc...ccxxcc...ccxxxcc...ccxxcc...ccx
cc.2...cccc.A...ccccc.e...cccc...2.cc
cc..2..SSSS..<.WwwwwwW....ssss..2..cc
cc...2.cccc.....ccBcc...e.cccc.2...cc
xcc...ccxxcc.W.ccxCxcc.W.ccxxcc...ccx
xxccUccxxxxccwccxx.xxccwccxxxxccuccxx
xxxcUcxxxxxxcwcxx1e.xxcwcxxxxxxcucxxx
xxxcUcxxxxxxcwDE.e<e.FGwcxxxxxxcucxxx
xxxcUcxxxxxxcwcxx.e1xxcwcxxxxxxcucxxx
xxccUccxxxxccwccxx.xxccwccxxxxccuccxx
xcc...ccxxcc.W.ccxHxcc.W.ccxxcc...ccx
cc...2.cccc.e...ccIcc.....cccc.2...cc
cc..2..TTTT....WwwwwwW.<..gggg..2..cc
cc.2...cccc...e.ccccc.....cccc...2.cc
xcc...ccxxcc...ccxxxcc...ccxxcc...ccx
xxccJccxxxxccVccxxxxxcchccxxxxcccccxx
xxxxKxxxxxxxcVcxxxxxxxchcxxxxxxxxxxxx
xxxxKxxxxxxxcVcxxxxxxxchcxxxxxxxxxxxx
xxxxKxxxxxxccVccxxxxxcchccxxxxxxxxxxx
xxxx.Xxxxxcc...ccxxxcc...ccxxxxxxxxxx
xxX.e.Xxxxc...2.ccccc.2...cxxxxxxxxxx
xxX1.e.MMML..2..YYYYY..2..cxxxxxxxxxx
xxX.1.xxxxc.2...ccccc...2.cxxxxxxxxxx
xxxXXXxxxxcc...ccxxxcc...ccxxxxxxxxxx
xxxxxxxxxxxcccccxxxxxcccccxxxxxxxxxxx
xxxxxxxxxxxxcccxxxxxxxcccxxxxxxxxxxxx
ENDMAP

############################################################################
# Tree sewer. by co
# This sewer is structured as a tree, in the sense that there are no loops.
# At each endpoint there is  monster or loot.
# At least you won't go in circles...
#
NAME:   sewer_co_tree
TAGS:   no_item_gen no_monster_gen no_pool_fixup
ORIENT: encompass
MONS:   kobold / big kobold
MONS:   adder / ball python / worm
MONS:   ooze / rat / sewer rat w:5
# less water in the middle, more at the endpoints.
SUBST:  ~ = WWWW.
SUBST:  - = WW...
SUBST:  " = W...
NSUBST: ? = 4=ef / *=112233
KITEM:  e = potion of curing / potion of heal wounds / potion of might /\
        potion of magic / potion of haste w:5 / potion of brilliance w:5
KITEM:  f = scroll of teleportation / scroll of fear w:5 /\
        scroll of blinking w:3 / scroll of magic mapping w:3 /\
        scroll of fog w:3
: sewer_setup(_G)
MAP
xxxxxxxxxxxxxxcccccccccccccccccccccxxxxxxxxxxxxxx
xxxxxxxxxxxxxxc?c?c?c?cxxxc?c?c?c?cxxxxxxxxxxxxxx
xxxxxxxxxxxxxxcWcWcWcWcxxxcWcWcWcWcxxxxxxxxxxxxxx
xxxxxxxxxxxxxxcWcWcWcWcxxxcWcWcWcWcxxxxxxxxxxxxxx
xxxxxxxxxxxxxxcWcWcWcWcxxxcWcWcWcWcxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx~~~x~~~xxxxx~~~x~~~xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx~xxx~xxxxxxx~xxx~xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx~xxx~xxxxxxx~xxx~xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx~xxx~xxxxxxx~xxx~xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx~~~~~xxxxxxx~~~~~xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx~~~xxxxxxxxx~~~xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx-~-xxxxxxxxx-~-xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx-~-xxxxxxxxx-~-xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx-~-xxxxxxxxx-~-xxxxxxxxxxxxxxxxx
cccccxxxxxxxxxxxx-~--xxxxxxx--~-xxxxxxxxxxxxccccc
c?WWW~xxxxxxxxxxx-~~--xxxxx--~~-xxxxxxxxxxx~WWW?c
ccccc~~~~~xxxxxxxx--~-~~~~~-~--xxxxxxxx~~~~~ccccc
c?WWW~xxx~~-----xxxx-~~~~~~~-xxxx-----~~xxx~WWW?c
cccccxxxx~~~~~~~-xxxx---~---xxxx-~~~~~~~xxxxccccc
c?WWW~xxx~~----~-xxxx~--~--~xxxx-~----~~xxx~WWW?c
ccccc~~~~~xxxx--~-xx~---~---~xx-~--xxxx~~~~~ccccc
c?WWW~xxxxxxxxx--~-~----~----~-~--xxxxxxxxx~WWW?c
cccccxxxxxxxxxxx~~-----"""-----~~xxxxxxxxxxxccccc
cxxxxxxxxxxxxxxx~~----"..."----~~xxxxxxxxxxxxxxxc
cxxxxxxxxxxxxxxx~~~~~~"A.<"~~~~~~xxxxxxxxxxxxxxxc
cxxxxxxxxxxxxxxx~~----"..."----~~xxxxxxxxxxxxxxxc
cccccxxxxxxxxxxx~~-----"""-----~~xxxxxxxxxxxccccc
c?WWW~xxxxxxxxx--~-~----~----~-~--xxxxxxxxx~WWW?c
ccccc~~~~~xxxx--~-xx~---~---~xx-~--xxxx~~~~~ccccc
c?WWW~xxx~~----~-xxxx~--~--~xxxx-~----~~xxx~WWW?c
cccccxxxx~~~~~~~-xxxx~--~---xxxx-~~~~~~~xxxxccccc
c?WWW~xxx~~-----xxxx-~~~~~~~-xxxx-----~~xxx~WWW?c
ccccc~~~~~xxxxxxxx--~-~~~~~-~--xxxxxxxx~~~~~ccccc
c?WWW~xxxxxxxxxxx-~~--xxxxx--~~-xxxxxxxxxxx~WWW?c
cccccxxxxxxxxxxxx-~--xxxxxxx--~-xxxxxxxxxxxxccccc
xxxxxxxxxxxxxxxxx-~-xxxxxxxxx-~-xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx-~-xxxxxxxxx-~-xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx-~-xxxxxxxxx-~-xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx~~~xxxxxxxxx~~~xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx~~~~~xxxxxxx~~~~~xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx~xxx~xxxxxxx~xxx~xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx~xxx~xxxxxxx~xxx~xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx~xxx~xxxxxxx~xxx~xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx~~~x~~~xxxxx~~~x~~~xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxcWcWcWcWcxxxcWcWcWcWcxxxxxxxxxxxxxx
xxxxxxxxxxxxxxcWcWcWcWcxxxcWcWcWcWcxxxxxxxxxxxxxx
xxxxxxxxxxxxxxcWcWcWcWcxxxcWcWcWcWcxxxxxxxxxxxxxx
xxxxxxxxxxxxxxc?c?c?c?cxxxc?c?c?c?cxxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccccccccccccccccccccxxxxxxxxxxxxxx
ENDMAP

############################################################################
# Cracked pipes. by co
# Most of the time the player will move between the pipes through cracks,
# these will be filled with rats. Only two of the paths will be generated
# each map, but all three pipes will be accessible on all instances.
#
NAME:    sewer_co_cracked_pipes
TAGS:    no_item_gen no_monster_gen no_pool_fixup no_rotate no_vmirror
ORIENT:  encompass
#      water creatures
MONS:    cinder newt / ball python w:5 / \
         generate_awake cinder newt / generate_awake ball python w:5 / \
         adder w:2 / nothing w:4
KMONS:   2 = adder
KFEAT:   2 = W
COLOUR:  2 = lightgreen
#      generate first path.
SHUFFLE: BC/DE/FG
NSUBST:  B = 1:. / *:c
NSUBST:  C = 1:. / *:c
SUBST:   DEFG = c
#      generate second path.
SHUFFLE: HI/JK/LM
NSUBST:  H = 1:. / *:c
NSUBST:  I = 1:. / *:c
SUBST:   LMKJ = c
#      randomize environment
KPROP:   V2 = no_tele_into
SUBST:   . = .W
SUBST:   V = .WWWW
SUBST:   X = xx"
#      rat path
KPROP:   " = no_tele_into
KMONS:   " = nothing w:50 / rat w:15 / sewer rat w:7
KPROP:   w = no_tele_into
#      loot
SUBST:   e = !!!!!?????%
KITEM:   ? = potion of curing / potion of heal wounds / potion of might /\
         potion of magic / potion of haste w:5 / potion of brilliance w:5
KITEM:   ! =  scroll of teleportation / scroll of fear w:5 /\
         scroll of blinking w:3 / scroll of magic mapping w:3 /\
         scroll of fog w:3
: sewer_setup(_G)
MAP
 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
 xcccccccccccccccccccccccccccccccccccccccccccccccx
 xwwwwwwwwmwWe.....1......VVVVVVVVV......1.....<cx
 xwwwwwwwwmwWe.........1..VVVVVVVVV.1..........ecx
 xccccccccccccIIIIIIccccccccccKccccccMMMcccccMMMcx
 xxxxxxxxxxxxxx"xx"xxxxxxxxxxcVcxxxxxx"xxxxxxx"xxx
             xxx""xxxxxxxxxxxcVcxxxxxxx"xXxxx"xx
             xXxx"xxxxxxxxxxxcVcxxxxxxxx"xX""Xx
             xx""x"xxxxxxxxxxc2cxxxxxxxxx"x"xxx
             x"xxXxxxXxXxxxxxcVcxxxxxxxxxX"x"xx
             xx""""x"x"xxxxxxcVcxxxxxxxxx"xXx"xx
             xXxxxx"x"x"xxxxxcVcxxxxxxxxxx"xxx"x
 xxxxxxxxxxxxxxxxx"xxxxx"xxxxcVcxxxxxxxxxxx"Xx"xxxxxxxxxxxxx
 xcccccccccccccccHHHcccHHHccccJcccccccccccLLLLLLcccccccccccx
 xwwwwwwwwmwWe....1.......VVVVVVVVV........1...eWwmwwwwwwwwx
 xwwwwwwwwmwWe.........1..VVVVVVVVV..1.........eWwmwwwwwwwwx
 xccccccccccCCCccCCCccccccccccEccccccccccGGGcGGGcccccccccccx
 xxxxxxxxxxxx"xxxx"xxxxxxxxxxcVcxxxxxxxxxx"xxx"xxxxxxxxxxxxx
          xxxx"xx"xxxxxxxxxxxcVcxxxxxxxxxxx"x"xx
          xxxx"x"xxxxxxxxxxxxcVcxxxxxxxxxxxx""Xx
          xX""x"xXxxxxxxxxxxxc2cxxxxxxxXx"xX"xxx
          xxxx"xxxxxxxxxxxxxxcVcxxxxxxxx"xx"x"xx
           xxx""xXxXxxxxxxxxxcVcxxxxxxxx"x"xxx"x
           xxXxx"""xxxxxxxxxxcVcxxxxxxxxx"xxxXxx
           xxxx"xxx"xxxxxxxxxcVcxxxxxxxx"x"xxxxxxxxxxxxxxxxx
           xccBBBcBBBcccccccccDccccccccFFFFFcccccccccccccccx
           xc<............VVVVVVVVV.........1..eWwmwwwwwwwwx
           xcA........1...VVVVVVVVV.....1......eWwmwwwwwwwwx
           xcccccccccccccccccccccccccccccccccccccccccccccccx
           xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

############################################################################
# Scattered pipes. by co
# The loot is often at the end of the pipes, with guarding monsters.
#
NAME:    sewer_co_scattered_pipes
TAGS:    no_item_gen no_monster_gen no_pool_fixup
ORIENT:  encompass
KMONS:   P = plant / nothing
#      pipe monsters.
KFEAT:   1 = W
COLOUR:  1 = lightgreen
KMONS:   1 = cinder newt / ball python w:5 / generate_awake ball python w:5 / \
         adder w:7
SUBST:   1 = 1111W
#      other monsters
KMONS:   2 = jackal / kobold, w:5 big kobold / w:5 sewer rat, rat
SUBST:   2 = 222222W.
#      loot
SUBST:   e = !!!?????%WWW
KFEAT:   !?% = W
COLOUR:  !?% = lightgreen
KITEM:   ? = potion of curing / potion of heal wounds / potion of might /\
         potion of magic / potion of haste w:5 / potion of brilliance w:5
KITEM:   ! =  scroll of teleportation / scroll of fear w:5 /\
         scroll of blinking w:3 / scroll of magic mapping w:3 /\
         scroll of fog w:3
SUBST:   . = .W
SUBST:   X = x.
SUBST:   Y = xxw
#
NSUBST:  B = 1:< / *:.
: sewer_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxYxcccxxcccYYYxxxxxYYYxxxxxxxxxxxxxxx
xxxxxxYYwwcecwwcccwwwwYYYwwwwwYYYYwYxxxxxxxx
xxxxxYwwwwcWcwwcccwwwwwwwwwwwwwwwwwwwYxxxxxx
xxxxYwwwwwcWcwwcccwwwwwwwwwwwwwwwwwwwwYxxxxx
xxxxwwwww.cWcPPcccwwwwwwwwwwwwwwwwwwwwwYxxxx
xxxxww....cWcPPcecwwwwwwwwwwwwwwwwwwwwwYxxxx
xxxX...A<.PPPPxcWcccccccc.wwwwwwwwwwwwwwYxxx
xxx........PxxxcWceW1WWWW..wwwwwwwwwwwwwYxxx
xxx..Pcccccccccc1cccccccc..wwwwwww...wwwwYxx
xxxPPPWWWWW1WWecWPxxexxxX..wwwwww..e..wwwYxx
xxxPPPccccccccccWcPx2xxx...wwwwww.2B2.wwwYxx
xxx.....XxxxxxxcWc..xxxX...wwwwww..P..wwwYxx
xxxX.....XxxxxxcWcxxxxX....wwwwww.PPP.wwwYxx
xxxxX.....XxxxxcWcxxxX....wwwwwww.cWc.wwwYxx
xxxxX..2.....XxcWcxxX.....wwwwwwwwcWcwwwwYxx
xxxx....cWc....cWcX.......wwwwwwwwcWcwwwwYxx
xxxP.2..cWc..........2..Pccccccccwc1cwwwYxxx
xxxPP...c1c...P..2.....P.PwwwwwwwwcWcwwwYxxx
xxxPPP..cWc..PPP.......PPccccccccwcWcwwwwYxx
xxxXPPP.cec2.cWc.......P.wwwwwwwwwcWcwwwYxxx
xxxX.cccccccccWc.......P.wwwwwwwwwcWcwwwwYxx
xxxX.WWWW1WWecWc......P..wwwwwwwwwcWcwwwwxxx
xxxX.cccccccccWc.2...P..wwwwwwwwwPcWc.wwwYxx
xxxXXxxX.2.xPP1c....P...wwwwwwwwPPPPPP..wYxx
xxxxxxx.e.xPxcWc...PPPccccccccccccPPP...Xxxx
xxxxxxxxXXxxxcWc....PPWWWWW1WWW1WWPP..2.Xxxx
xxxxxxxxxxxxxcWc..2..PccccccccccccP..e..Xxxx
xxxX...XxxxxxcWc........wwwwwwww........Xxxx
xxX.2...Xxxx............wwwwwwww....PP..Xxxx
xxX...PcccccccccccPP....wwwwwwww.2.PPP.Xxxxx
xxX2ePPWWWWW1WWWWWP...wwwwwwwww....cWcXxxxxx
xxX..P.ccccccccccc.Pwwwwwwwwwww..XXcWcxxxxxx
xxxX....wwwwwwwwwwwwwwwwwwwwwww.XxxcWcxxxxxx
xxxYwwwwwwwwwwwwwwwwwwwwwwwwwwYxxxxc1cxxxxxx
xxxYwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxcWcxxxxxx
xxxYwwwwwwwwwwwwwwwwwwwwwwwwwYxxxX.cWc.Xxxxx
xxxxYwwwwwwwwwwwwwwwwwwwwwwwYxxxX..PPP..Xxxx
xxxxxwwwwwwwwwwwwwwwwwwwwwwxxxxxX..PB.2.Xxxx
xxxxxYwwwwwwwwwwwwwwwwwwwwYxxxxxX.2..e..Xxxx
xxxxxxYwwwwwwYYYYwwwYYYwwYxxxxxxxX.....Xxxxx
xxxxxxxYYYxYYxxxxYYYxxxYYxxxxxxxxxxXXXxxxxxx
ENDMAP

#############################################################
# Kobolds who escaped to the sewers to worship Makhleb in peace
# The temple has 3 sealed chambers at the end, each with loot and a
# boss monster.
# Total loot: 8 ok items (mostly consumables), 4 star items
NAME:    kennysheep_sewer_church
TAGS:    no_item_gen no_monster_gen no_pool_fixup
ORIENT:  encompass
WEIGHT:  3
KFEAT:   B = altar_makhleb
MARKER:  F = lua:fog_machine { cloud_type = "flame", pow_min = 100, \
                               pow_max = 100, delay = 10, size = 1, \
                               walk_dist = 0, excl_rad = 0 }
: sewer_setup(_G)
# Placed monsters.
MONS:    kobold w:30 / shadow imp / big kobold zombie /\
         ufetubus / nothing w:5
# Randomly generated monsters.
MONS:    kobold w:50 / kobold zombie / ooze / giant cockroach / \
         giant cockroach zombie w:3 / adder / adder zombie w:3 / \
         bat / bat zombie w:3
SUBST:   . = .:175 2
# Sub-boss.
KMONS:   3 = big kobold w:15 / nothing w:5
# Bosses.
KMONS:   4 = big kobold
KMONS:   5 = Grinder, shadow imp
KMONS:   6 = quasit
SHUFFLE: 456
KITEM:   456 = star_item
KFEAT:   - = deep_water
KPROP:   W- = no_tele_into
SUBST:   W = WWWw.
SUBST:   - = wwwW-
COLOUR:  w- = green
COLOUR:  W = lightgreen
ITEM:    potion of curing / potion of heal wounds / \
         potion of might w:2 / potion of brilliance w:2 / \
         potion of agility w:2 / scroll of random uselessness w:14 / \
         any scroll w:7 / any armour w:2 / any weapon w:2
NSUBST:  d = 1:*
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xwwwxwwwwwww---xWWWx---wwwwww----WWxWWWx  ccc
xwwwwwwwwwww----W.WW---wwwwww----W..WWWx  c<c
xwwwxwwwwwww---xWW.x---wwwwww----WWx111x ccdcc
xx-xxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxx1xx cd4dc
x---x          x-W.xwwwwwwwwwwww-W.+.W-xcc1.1cc
xWWWx          x-W.xwwwwwwwwwwww-W.x.W-xc.....c
x.<.x          x-W.xwwwwwwwwwwww-W.x.W-xc.lll.c
x...x          x-W.xwwwwwwwwwww--W.x.W-xc.....c
xWWWx          x-W.xwwwwwwwwww--W..x.W-xcc+++cc
x---x          x-W.x-----------W...x.W-xcc...ccccccc
xx-xxxxxxxxxxxxxx.xxxx-xx-xx-xxx.xxxx.xcc..F..cc...ccc
xwwwxwwwwwww---x-W.xW..........W1w-xW..c..2.2F.+.l.1dccc
xwwwwwwwwwww-----W...wwwwwwwwww1ww--W..+.F.B3..+.l..5d<c
xwwwxwwwwwww---x-W.xW..........W1w-xW..c..2.2F.+.l.1dccc
xxxxxxxxxxxxxxxxx.xxxxx-x--xxx-x.xxxx.xcc..F..cc...ccc
               x-W.x-----------W...x.W-xcc...ccccccc
               x-W.xwwwwwwwwww--W..x.W-xcc+++cc
               x-W.xwwwwwwwwwww--W.x.W-xc.....c
               x-W.xwwwwwwwwwwww-W.x.W-xc.lll.c
               x-W.xwwwwwwwwwwww-W.x.W-xc.....c
               x-W.xwwwwwwwwwwww-W.+.W-xcc1.1cc
xxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxx1xx cd6dc
x--------------x-W1x---wwwwwww---WWx111x ccdcc
xWWWWWWWWWWWWWW.WW.W---wwwwwww---W..W.Wx  c<c
x..............x1.Wx---wwwwwww---WWxWWWx  ccc
xxx.xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xx...xx
x.....x
x..A..x
x..<..x
xx...xx
 xxxxx
ENDMAP
