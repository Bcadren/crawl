###############################################################################
# slime.des: All maps and branch endings relating to the Slime Pits
###############################################################################

{{
function fixup_slime_vaults(data, triggerable, triggerer, marker, ev)
  if not dgn.persist.fix_slime_vaults then
    return
  end

  local seen = dgn.seen_destroy_feat("clear_stone_wall")

  if you.god() == "Jiyva" and you.piety_rank() >= 6 then
    if you.silenced() then
        crawl.mpr("You feel a strange vibration for a moment.", "god")
    else
        crawl.mpr("You hear the sound of toppling stones.", "god")
    end
  else
    if seen then
        crawl.mpr("The stone walls suddenly crumble and collapse.",
                  "monster_enchant")
    else
        crawl.mpr("You feel a strange vibration for a moment.",
                  "monster_enchant")
    end
  end

  local gxm, gym = dgn.max_bounds()
  for p in iter.rect_iterator(dgn.point(1, 1), dgn.point(gxm-1, gym-1)) do
    if dgn.fprop_at(p.x, p.y, "no_jiyva") then
      dgn.fprop_changed(p.x, p.y, "no_jiyva")
    end
  end

  -- We're done.
  dgn.persist.slime_fixup_done = true

  triggerable:remove(marker)
end
}}

##############################################################################
# Slime Pit entry vaults
##############################################################################

# Weight balancer
NAME:  slime_dummy
TAGS:  slime_entry extra
KFEAT: O = enter_slime_pits
MAP
O
ENDMAP

# Slime entries featuring an altar to Jiyva
NAME:   slime_altar_1
TAGS:   slime_entry extra
WEIGHT: 30
KFEAT:  Z = altar_jiyva
KFEAT:  O = enter_slime_pits
KFEAT:  b = slimeshroom
KFEAT:  a = slimy_water
SHUFFLE: OZ
SHUFFLE: ab
MAP
baab
.OZ.
....
ENDMAP

NAME:   slime_island
TAGS:   slime_entry extra
WEIGHT: 30
KFEAT:  Z = altar_jiyva
KFEAT:  O = enter_slime_pits
NSUBST: . = 8:b / *:a
KFEAT:  b = slimeshroom
KFEAT:  a = slimy_water
KFEAT:  A = floor
MAP
  ......
 ........
..........
...bAAb...
...AOZA...
...bAAb...
.....A....
 .....A..
  ...@..
ENDMAP

NAME:   slime_altar_2
TAGS:   slime_entry extra
KFEAT:  _ = altar_jiyva
KMONS:  0 = jelly / ooze
KMONS:  1 = acid blob / slime creature w:5
KMONS:  O = azure jelly
: if crawl.coinflip() then
SUBST:  _ = 1
SUBST:  - = _
: else
SUBST:  - = 0
: end
KFEAT:  O = enter_slime_pits
KFEAT:  01 = slimy_water
KFEAT:  t = slimeshroom
MAP
ccccc
ct10q.
cO_-=@
ct10q.
ccccc
ENDMAP

NAME:  dpeg_slime_entry_enclosed_altar
TAGS:  slime_entry extra
KFEAT: O = enter_slime_pits
KFEAT: S = slimy_wall
KFEAT: _ = altar_jiyva
NSUBST: ' = 4:s / *:'
KFEAT: ' = slimy_water
KFEAT: s = slimeshroom
MAP
  .....
 .ccccc.
.ccSSScc.
.cS'''Sc.
.cS'O'cc.
.cS''_.+@
.ccScccc.
.ccccccc.
 .ccccc.
  .....
ENDMAP

NAME:   minmay_slime_entry_eyes
TAGS:   slime_entry ruin_lair extra
WEIGHT: 2
MONS:   eye of devastation, eye of draining
KFEAT:  O = enter_slime_pits
SUBST:  x = xxS
SUBST:  ' = aaab..
KFEAT:  a = slimy_water
KFEAT:  b = slimeshroom
KFEAT:  S = slimy_wall
MAP
 ....   ....
..xx..x..xx..
.xxxx.x.xxxx.
.xxx'''''xxx.
..x'S'''S'x..
 ..'''''''..
 xxS'1O2'Sxx
 ..'''''''..
..x'S'''S'x..
.xxx'''''xxx.
.xxxx.x.xxxx.
..xx..x..xx..
 ....   ....
ENDMAP

NAME:    bcadren_minmay_slime_entry_pools
TAGS:    slime_entry ruin_lair extra
MONS:    jelly / slime creature
KFEAT:   O = enter_slime_pits
SHUFFLE: ABCD
SUBST:   A = c
SUBST:   B = d
SUBST:   C = aa.
SUBST:   D = bba
KFEAT:   a = slimeshroom
KFEAT:   b = slimy_water
KFEAT:   d = slimy_wall
MAP
..A.....D..
.AAA...DDD.
AAAAA1DDDDD
.AAA...DDD.
..A.....D..
..1..O..1..
..B.....C..
.BBB...CCC.
BBBBB1CCCCC
.BBB...CCC.
..B.....C..
ENDMAP

NAME:    minmay_slime_entry_pillars
TAGS:    slime_entry ruin_lair extra
MONS:    jelly / slime creature
KFEAT:   O = enter_slime_pits
SUBST:   A : cd , B : cd , C : cd , D : cd
KFEAT:   d = slimy_wall
MAP
...........
.AAA...DDD.
.AAA.1.DDD.
.AAA...DDD.
...........
..1..O..1..
...........
.BBB...CCC.
.BBB.1.CCC.
.BBB...CCC.
...........
ENDMAP

# In-game the boxes rarely stay closed due to ruination; this is intentional.
NAME:   minmay_slime_entry_jelly_boxes
TAGS:   slime_entry ruin_lair extra
WEIGHT: 5
KMONS:  b = jelly
NSUBST: ABCD = 1:a / 3:b
KFEAT:  a = slimy_water
KFEAT:  b = slimeshroom
KFEAT:  s = slimy_wall
KFEAT:  O = enter_slime_pits
MAP
.............
.xxxxx.xxxxx.
.xxxAx.xDDxx.
.xxAAx.sxDxx.
.xxAs...sDxx.
.xxss....sxx.
......O......
.xxss...ssxx.
.xxB.s.s.Cxx.
.xBBx.xxCCxx.
.xxBx.xxxCxx.
.xxxxx.xxxxx.
.............
ENDMAP

NAME:   minmay_slime_entry_oklob_open
TAGS:   slime_entry extra
WEIGHT: 5
KMONS:  1 = oklob plant
KFEAT:  1c = slimy_water
KFEAT:  O = enter_slime_pits
NSUBST: 1 = 2:1 / *:c
NSUBST: c = 2:b / *:c
KFEAT:  b = slimeshroom
MAP
 .....
..ccc@.
.c11@c.
.c1O1c.
.c111c.
..ccc..
 .....
ENDMAP

NAME:   minmay_slime_entry_oklob_enclosed
TAGS:   slime_entry extra
ORIENT: northeast
WEIGHT: 5
KMONS:  1 = oklob plant
NSUBST: 1 = 2:1 / *:s
KFEAT:  S = slimy_wall
SUBST:  s = sssb
KFEAT:  b = slimeshroom
KFEAT:  1s = slimy_water
KFEAT:  O = enter_slime_pits
MAP
xxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxx
xxxSSSSxx.......xxxxxxx
xxSS11....ssssss.xxxxxx
xSS1...ssss....ss.xxxxx
xS1.sss....xx..ss.xxxxx
xSssOs..11xx..ss.xxxxxx
xSSss.11Sxx.ssss.xxxxxx
xxSSSSSSxx.ssss.xxxxxxx
xxxxxxxxx.sss..xxxxxxxx
xxxxxxxx.ss..xxxxxxxxxx
xxxxxxx@ss@xxxxxxxxxxxx
ENDMAP

NAME:   dpeg_slime_entry_corner
TAGS:   slime_entry ruin_lair
ORIENT: northwest
WEIGHT: 5
KFEAT:  O = enter_slime_pits
KFEAT:  S = slimy_wall
KFEAT:  s = slimy_water w:2 / floor w:1
MAP
xxxxxxxxxxx
xSSSxxxxxx
xSsssxxxx
xSsO..sxx
xxxxxx.xx
xxxxs..sx
xxx...xx.
xx..
ENDMAP

NAME: nicolae_slime_entry_slimehenge
TAGS: slime_entry transparent
NSUBST: A = . / A, B = . / B, C = . / C
SHUFFLE: ABCD
KFEAT: O = enter_slime_pits
KMONS: 1 = ooze / jelly / nothing
KFEAT: A = slimy_wall
: if crawl.one_chance_in(3) then
KFEAT: BCD = floor
:else
KFEAT: B = slimeshroom
KFEAT: CD = slimy_water
:end
MAP
  .........
 ...........
.....DAD.....
...A.CBC.A...
....B.1.B....
..DC.1.1.CD..
..AB1.O.1BA..
..DC.1.1.CD..
....B.1.B....
...A.CBC.A...
.....DAD.....
 ...........
  .........
ENDMAP

################################################################################
# Slime Pit specific non-ending non-entry vaults
################################################################################
NAME: nicolae_slime_pool
DEPTH: Slime, !Slime:$
TAGS: transparent
MONS: slime creature
ORIENT: float
SHUFFLE: abcd
SUBST: ab : @, cd : .
KPROP: f = no_tele_into
SUBST: f = w.1
KMASK: w = no_monster_gen
KMASK: W = no_monster_gen
{{
local pool_contents = {}
pool_contents["w"] = 75
pool_contents["W"] = 20
pool_contents["t"] = 5
add_pools { replace = ".",
			pool_size = 4 + crawl.random2(4),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
   cccc
 ..........
d.w.wwwf..w.
d..wwfwwf....
d.fwwfffwffbb
d..fwwwfww.b
 ...ffwwww.b
  ..wffw...b
  ..wfwfw.fw
 ...ffffw.ww
.ff.wwwwf.f
 .....f..ff
    aaaa..fw
ENDMAP

NAME: nicolae_slime_jelly_donut
DEPTH: Slime, !Slime:$
ORIENT: float
SHUFFLE: abcdefgh
SUBST: a = @, b : @., c : @.., d : @..., efgh = .
KMONS: j = jelly
{{
local pool_contents = {}
pool_contents["w"] = 75
pool_contents["W"] = 20
pool_contents["t"] = 5
add_pools { replace = ".",
			pool_size = 4 + crawl.random2(4),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
      aaaaa
    hh.....bb
   h.........b
  h...........b
 h....x.x.x....b
 h...x.x.x.x...b
g...x.x.x.x.x...c
g....x.jjj.x....c
g...x.xjjjx.x...c
g....x.jjj.x....c
g...x.x.x.x.x...c
 f...x.x.x.x...d
 f....x.x.x....d
  f...........d
   f.........d
    ff.....dd
      eeeee
ENDMAP

NAME: nicolae_slime_barrier
TAGS: transparent extra
DEPTH: Slime, !Slime:$
SHUFFLE: abcde
SUBST: a = @, bcde = .
{{
local pool_contents = {}
pool_contents["w"] = 75
pool_contents["W"] = 20
pool_contents["t"] = 5
add_pools { replace = ".",
			pool_size = 8 + crawl.random2(8),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
  aaaaa
 .........
 c..x.....b
c..........b
c..xxx..x..b
c..........b
 c..x.......b
 .......x....e
  ....x.x.x...e
   .....x......eee
    d.......x.....d
     d.............d
     d..x..xxx..x..d
     d.............d
      d.....x.......d
       eee......x.....
          e...x.x.x....
           e....x.......
            b.......x..c
             b..........c
             b..x..xxx..c
             b..........c
              b.....x..c
               .........
                  aaaaa
ENDMAP

NAME: nicolae_slime_welcoming_committee
TAGS: no_monster_gen no_item_gen no_trap_gen
DEPTH: Slime:2-, !Slime:$
SUBST: X = x., { = {[(
KMONS: 1 : jelly w:20 / ooze / slime creature w:30 / \
   acid blob w:5 / death ooze w:5 / azure jelly w:5
{{
local pool_contents = {}
pool_contents["w"] = 75
pool_contents["W"] = 20
pool_contents["t"] = 5
add_pools { replace = ".",
			pool_size = 4 + crawl.random2(4),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
      xxx
   xxxxxxxx
 xxxxXXX..1@
xxxXX.....1.@
xxX.......1..@
xxX.{.....1...@
xxX.......1..@
xxxXX.....1.@
 xxxxXXX..1@
   xxxxxxxx
      xxx
ENDMAP

NAME: nicolae_slime_phagocytosis
DEPTH: Slime, !Slime:$
TAGS: transparent
SUBST: a : .x, b : .x, c : .x, d : .x, e : .x, { = {[(, X = Wx, ' = ._
NSUBST: W = 4:1 / *:W
KMONS: 1 = slime creature
KFEAT: 1 = W
CLEAR: _
{{
local pool_contents = {}
pool_contents["w"] = 75
pool_contents["W"] = 20
pool_contents["t"] = 5
add_pools { replace = ".",
			pool_size = 8 + crawl.random2(4),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
       '.......'
      '..........'
    '.....................'
 '........ee...............'
'........exxe...............'
.......eexxxxeee.xxxxxxx.....'
....ddxxxxxxxxxxxxxxxxxxx.....
@..dxxxxxXXXXxxxxx.....xxx....
@..dxxxxXWWWWXx.........xxx...
@...ddxxXWWWW............xx...
@.....cxXWWWW....{..x....x....
@.....cxXWWWWXx.....xx........
'....cxxxXXXXxxx...xxx.......'
 ...cxxxxxxxxxxxxxxxx.......'
 ...cxxxxabxxxxxxxxx.......
 ....axxa..bxxxb.........'
 .....aa....bbb.........'
 '....................'
  '...............'
   ''...........''
ENDMAP

NAME: nicolae_slime_round_cave
DEPTH: Slime:2-, !Slime:$
TAGS: no_monster_gen
SHUFFLE: {}/()/[]
NSUBST: a = X / .
SUBST: X = x., z = x_, 0 = 0009.., 9 = 9998
CLEAR: _
{{
local pool_contents = {}
pool_contents["w"] = 75
pool_contents["W"] = 20
pool_contents["t"] = 5
add_pools { replace = ".",
			pool_size = 4 + crawl.random2(4),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
       zzzzzzzzzzz
     zzzXXXXXXXXXzzz
    zzXXX.X.X.X.XXXzz
   zzXX...........XXzz
  zzXX...0..0..0...XXzz
 zzXX..0............XXzz
 zXX.....XXX.XXX..0..XXz
zzX..0..XXxx.xxXX.....Xzz
zXX....XXxxX.XxxXX....XXz
zX..0.XXxxXX.XXxxXX.9..Xz
zXX..aXxxXX...XxxxX...XXz
zX....XxXX.............Xz
zXX.{.......}....9..0.XXz
zX....XxXX.............Xz
zXX..aXxxXX...XxxxX...XXz
zX..0.XXxxXX.XXxxXX.9..Xz
zXX....XXxxX.XxxXX....XXz
zzX..0..XXxx.xxXX.....Xzz
 zXX.....XXX.XXX..0..XXz
 zzXX..0............XXzz
  zzXX...0..0..0...XXzz
   zzXX...........XXzz
    zzXXX.X.X.X.XXXzz
     zzzXXXXXXXXXzzz
       zzzzzzzzzzz
ENDMAP

NAME: nicolae_slime_ancient_tower_intersection
DEPTH: Slime, !Slime:$
WEIGHT: 2
TAGS: extra decor transparent
SUBST: c = ccc., X = x_, V = V..
CLEAR: _
{{
local pool_contents = {}
pool_contents["w"] = 75
pool_contents["W"] = 20
pool_contents["t"] = 5
add_pools { replace = ".",
			pool_size = 4 + crawl.random2(8),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
  XXXX         XXXX
 XXxxXX       XXxxXX
XXxxxxxx@@@@@xxxxxxXX
Xxxxx...........xxxxX
Xxxx.............xxxX
XXx...............xXX
 Xx.....ccccc.....xX
  x....cc...cc....x
  @...cc.....cc...@
  @...c..V.V..c...@
  @...c.......+...@
  @...c..V.V..c...@
  @...cc.....cc...@
  x....cc...cc....x
 Xx.....ccccc.....xX
XXx...............xXX
Xxxx.............xxxX
Xxxxx...........xxxxX
XXxxxxxx@@@@@xxxxxxXX
 XXxxXX       XXxxXX
  XXXX         XXXX
ENDMAP

NAME: cheibrodos_slime_lost_civilization_1
DEPTH: Slime, !Slime:$
TAGS: decor transparent ruin
ORIENT: float
SUBST: B = xxxW., C = W., D = VVW.
{{
local pool_contents = {}
pool_contents["w"] = 75
pool_contents["W"] = 20
pool_contents["t"] = 5
add_pools { replace = ".",
			pool_size = 8 + crawl.random2(8),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx
xx..............@
xx..............@
xx..x.......x...@
xx.....BBB......@
xx....CCBCC....xx
xx...BCDBDCB...xx
xx...BBBBBBB...xx
xx...BCDBDCB...xx
xx....CCBCC....xx
@......BBB.....xx
@...x.......x..xx
@..............xx
@..............xx
xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx
ENDMAP

NAME: cheibrodos_slime_lost_civilization_2
DEPTH: Slime, !Slime:$
TAGS: decor
ORIENT: float
{{
local pool_contents = {}
pool_contents["w"] = 75
pool_contents["W"] = 20
pool_contents["t"] = 5
add_pools { replace = ".",
			pool_size = 4 + crawl.random2(4),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
   xxxxx
  xxxxxxx
 xxx...xxx
xxx.....xxx
xx...{...xx
xx.G...G.xx
xx...(...xx
xxx.....xxx
xxxx...xxxx
 xxx...xxx
  xx@@@xx
ENDMAP

NAME: cheibrodos_slime_lost_civilization_3
DEPTH: Slime, !Slime:$
TAGS: decor transparent
ORIENT: float
SHUFFLE: W.
SUBST: W = WW.
KMASK: W = no_monster_gen
{{
local pool_contents = {}
pool_contents["w"] = 75
pool_contents["W"] = 20
pool_contents["t"] = 5
add_pools { replace = ".",
			pool_size = 4 + crawl.random2(4),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
xxxxxxxxxxxxx
xWWWxWWWxWWWx
xWWWxWWWxWWWx
xx.xxx.xxx.xx
@...........@
@...........@
@...........@
xx.xxx.xxx.xx
xWWWxWWWxWWWx
xWWWxWWWxWWWx
xxxxxxxxxxxxx
ENDMAP

NAME: cheibrodos_slime_lost_civilization_4
DEPTH: Slime, !Slime:$
TAGS: decor
MAP
.x...x.
.......
x..G..x
.......
.x...x.
ENDMAP

NAME: cheibrodos_slime_lost_civilization_5
DEPTH: Slime, !Slime:$
TAGS: decor
SUBST: A=x, B=.
SHUFFLE: AB
{{
local pool_contents = {}
pool_contents["w"] = 75
pool_contents["W"] = 20
pool_contents["t"] = 5
add_pools { replace = ".",
			pool_size = 4 + crawl.random2(8),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
     ...
    ..B..
   A.BBB.A
  AAA.B.AAA
 ..A.....A..
..B.......B..
.BBB..{..BBB.
..B.......B..
 ..A.....A..
  AAA.B.AAA
   A.BBB.A
    ..B..
     ...
ENDMAP

NAME: cheibrodos_slime_lost_civilization_6
DEPTH: Slime, !Slime:$
TAGS: decor
SUBST: } = }G..
{{
local pool_contents = {}
pool_contents["w"] = 75
pool_contents["W"] = 20
pool_contents["t"] = 5
add_pools { replace = ".",
			pool_size = 4 + crawl.random2(4),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
.............
.x.x.x.x.x.x.
..x.x.x.x.x..
.x.........x.
..x.......x..
.x...}.....x.
..x.......x..
.x.....{...x.
..x.......x..
.x.........x.
..x.x.x.x.x..
.x.x.x.x.x.x.
.............
ENDMAP

NAME: cheibrodos_slime_lost_civilization_7
DEPTH: Slime, !Slime:$
TAGS: decor
{{
local pool_contents = {}
pool_contents["w"] = 75
pool_contents["W"] = 20
pool_contents["t"] = 5
add_pools { replace = ".",
			pool_size = 4 + crawl.random2(4),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
  ...........
 .............
...............
.......x.......
.......0.......
.....x0V0x.....
.......0.......
.......x.......
...............
 .............
  ...........
ENDMAP

NAME: cheibrodos_slime_lost_civilization_8
DEPTH: Slime, !Slime:$
TAGS: decor
ORIENT: float
SUBST: - = xxx., " = ...x
{{
local pool_contents = {}
pool_contents["w"] = 75
pool_contents["W"] = 20
pool_contents["t"] = 5
add_pools { replace = ".",
			pool_size = 4 + crawl.random2(4),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
@.............
.(..----------
.......".....-
...".".".".".-
.-...........-
.-.".".".".".-
.-.....".....-
.-"".""xx..--x
.-.....xV....x
.-.".".......x
.-........)..x
.-.".".-.....x
.-.....-....Vx
.-------xxxxxx
ENDMAP

NAME: cheibrodos_slime_lost_civilization_9
DEPTH: Slime, !Slime:$
TAGS: decor transparent
ORIENT: float
SUBST: x = xxx., G = G..
{{
local pool_contents = {}
pool_contents["w"] = 75
pool_contents["W"] = 20
pool_contents["t"] = 5
add_pools { replace = ".",
			pool_size = 4 + crawl.random2(4),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
.............
.xx.xx.xx.xx.
.xxGxxGxxGxx.
.............
.............
@...........@
.............
.............
.xxGxxGxxGxx.
.xx.xx.xx.xx.
.............
ENDMAP

NAME: cheibrodos_slime_lost_civilization_10
DEPTH: Slime, !Slime:$
TAGS: decor transparent
ORIENT: float
SUBST: x = xxxx.
{{
local pool_contents = {}
pool_contents["t"] = 100
add_pools { replace = ".",
			pool_size = 4 + crawl.random2(4),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
...............
.xxxxxxxxxxxxx.
.x..V..x.....x.
.x.............
.x.....x......@
.x.....x.......
.xxxxxxx.....x.
.x.....x.....x.
.x...........x.
.x...........x.
.x...........x.
.xx...xxxxxxxx.
....@..........
ENDMAP

# inspired by a comment of spacejung on SomethingAwful (dpeg)
NAME:   dissolution_awaits
DEPTH:  Slime:2, Slime:3
KMONS:  1 = patrolling Dissolution, azure jelly / great orb of eyes
MAP
  {
  .
 .1.
[...(
ENDMAP

# Bcadren Original
NAME:   dissolution_ruined_church
DEPTH:  Slime: 4
TAGS:   place_unique no_monster_gen no_item_gen transparent
WEIGHT: 25
KMONS:  1 = patrolling Dissolution
KMONS:  2 = azure jelly / acid blob / death ooze
KMONS:  3 = slime creature
KFEAT:  1 = altar_jiyva
KFEAT:  a = slimy_water
NSUBST:  d : .aa
NSUBST:  b : aat
NSUBST:  c : 22333.
NSUBST:  x : xxxt.
MAP
ddddddddddddd
dxxxxxxxxxxxd
dxbbbtttbbbxd
dxb}..{..)bxd
dxbd.....dbxd
dxbddd1dddbxd
dxbdddddddbxd
dxbcccdcccbxd
dxbdddddddbxd
dxbcccdcccbxd
dxbdddddddbxd
dxbcccdcccbxd
dxbdddddddbxd
dxbbtt.ttbbxd
dxxxx...xxxxd
ddddd@@@ddddd
ENDMAP

################################################################################
# Slime Pit
################################################################################
NAME:    slime_pit
PLACE:   Slime:$
ORIENT:  encompass
SUBVAULT: o : royal_jelly
TAGS:    no_rotate no_dump
SUBST:   ' : ' .:1,  ' : ' x:1,  ' = .x
SUBST:   " : cc.aaa
KFEAT:   a = slimy_water
SHUFFLE: ([{
{{
local pool_contents = {}
pool_contents["w"] = 100
add_pools { replace = ".",
			pool_size = 16 + crawl.random2(16),
			contents = pool_contents,
			frequency = 10 }
}}
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx''''xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx''xxxx''''...''xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxx'''..'''''........'''xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxx'.....................'xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxx'.......................''x'xxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxx'..........................'.xxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx'............................'xxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx'.............................'''xxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx'...............................''xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx''..(..............................''xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx'''....................................''xxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxx'.........................................'xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx''.........................................'xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx'...........................................'xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxx''xx'..........................................'xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxx'..''................""""."""""...........]....''xxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxx'x.................."oooooooooo".................'xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxx'.'................"oooooooooooo".................''xxxxxxxxxxxxxxx
xxxxxxxxxxxxx'................."oooooooooooooo".................xxxxxxxxxxxxxxxx
xxxxxxxxxxxxx'................"oooooooooooooooo"................'xxxxxxxxxxxxxxx
xxxxxxxxxxxx''..............."oooooooooooooooooo"...............'xxxxxxxxxxxxxxx
xxxxxxxxxxxx'................"oooooooooooooooooo"................'''xxxxxxxxxxxx
xxxxxxxxxxxx'................"oooooooooooooooooo"..................xxxxxxxxxxxxx
xxxxxxxxxxx'................."oooooooooooooooooo"..................'xxxxxxxxxxxx
xxxxxxxxxxx'................."oooooooooooooooooo"..................''xxxxxxxxxxx
xxxxxxxxxxx'..................oooooooooooooooooo....................'xxxxxxxxxxx
xxxxxxxxxx'..........)......."oooooooooooooooooo"...................'xxxxxxxxxxx
xxxxxxxxxx'.................."oooooooooooooooooo"...................'xxxxxxxxxxx
xxxxxxxxxx'.................."oooooooooooooooooo"..................'xxxxxxxxxxxx
xxxxxxxxx'..................."oooooooooooooooooo"..................'xxxxxxxxxxxx
xxxxxxxxx''..................."oooooooooooooooo"..................'xxxxxxxxxxxxx
xxxxxxxxxxx'..................."oooooooooooooo"...................'xxxxxxxxxxxxx
xxxxxxxxxxxx'..................."oooooooooooo"....................'xxxxxxxxxxxxx
xxxxxxxxxxxx'...................."oooooooooo"....................'xxxxxxxxxxxxxx
xxxxxxxxxxxxx'...................."""".""""".....................'xxxxxxxxxxxxxx
xxxxxxxxxxxxx'...........................................[........'xxxxxxxxxxxxx
xxxxxxxxxxxx''..............................................'''....'xxxxxxxxxxxx
xxxxxxxxxxxx'.............................................''xxx''.'xxxxxxxxxxxxx
xxxxxxxxxxxxx'..........................................''xxxxxxx.xxxxxxxxxxxxxx
xxxxxxxxxxxxx'''.......................................'xxxxxxxxx.xxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx'......................................'xxxxxxxxx'xxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx'.......................................'xxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx''.....................................'xxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx'.....................................'xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx''....................................'xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx'...................................'xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx'............................}......'xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx''................................'xxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx'.............................''xxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx'.............................'xxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxx'......................''...'xxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxx'''''........{.......'xx'..xxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx'''............'xxxx''xxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx''....'....'xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx''''x...''xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx''''xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:    royal_jelly_end0
TAGS:    royal_jelly no_rotate
KMONS:   1 = the royal jelly
KMONS:   2 = acid blob
KFEAT:   2 = slimy_water / floor
KMONS:   3 = great orb of eyes / nothing
KFEAT:   3 = slimy_water / floor
SUBST:   4 = 23
SUBST:   ' : aaa....
NSUBST:  P = O / *|
KMASK:   |O* = no_monster_gen
KMASK:   C = no_pool_fixup
KPROP:   |O* = no_jiyva
KPROP:   |O* = no_tele_into
KFEAT:   |O* = slimy_water
KFEAT:   a = slimy_water
KFEAT:   Z = altar_jiyva
KITEM:   O = slimy rune of Zot
KITEM:   * = star_item
{{
local fixup_marker = TriggerableFunction:new(
  {
    func="fixup_slime_vaults",
    repeated=true
  }
)

fixup_marker:add_triggerer(DgnTriggerer:new {
  type="monster_dies",
  target="any"
})

fixup_marker:add_triggerer(DgnTriggerer:new {
  type="entered_level"
})

}}

# Doesn't matter where this marker goes, so we might as well choose
# the altar.
: lua_marker("Z", fixup_marker)

{{
    set_feature_name("stone_wall", "rune-carved stone wall")
    set_feature_name("clear_stone_wall", "rune-carved clear stone wall")
}}
MAP
    cccc2ccccc
   cc*cc''cc*cc
  cc***cc4c***cc
 cc*|*cc''cc*|*cc
cc*|P|*n4nn*|P|*cc
cc**|*nn''nn*|**cc
ccc**n|nn4n|n**ccc
ccccnnnn'3nnnncccc
c'4'n'4'1.'4'n'4'c
2'c'4'n'.Z'n'4'c'2
ccccnnnn3'nnnncccc
ccc**n|nn4n|n**ccc
cc**|*nn''nn*|**cc
cc*|P|*n4nn*|P|*cc
 cc*|*cc''cc*|*cc
  cc***cc4c***cc
   cc*cc''cc*cc
    cccc2ccccc
ENDMAP